
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ДокументОснование") Тогда
		Отправитель_Хозсубъект = Параметры.Отправитель_Хозсубъект;
		Отправитель_Площадка = Параметры.Отправитель_Площадка;
		Получатель_Хозсубъект = Параметры.Получатель_Хозсубъект;
		Получатель_Площадка = Параметры.Получатель_Площадка;
		ДокументОснование = Параметры.ДокументОснование;
		ИтогиПоПартиям = Ложь;
		ЗаполнитьПоДокументуОснованию();
	Иначе
		Отказ = Истина;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоДокументуОснованию()
	
	тзКПодбору = кб99_ВСД_Общий.ВыгрузитьТч(ДокументОснование);
	
	Если тзКПодбору.Колонки.Найти("Коэффициент") = Неопределено Тогда
		ВызватьИсключение "Функция ВыгрузитьТЧ была переопределена, необходимо переопределить функцию ВыгрузитьТЧ под новую версию интеграции";
	КонецЕсли;
	
	тзКПодбору.Свернуть("Номенклатура, Продукция_Элемент, Коэффициент", "Количество, Упаковки");
	тчКПодбору.Загрузить(тзКПодбору);
	
	Для Каждого Стр Из тчКПодбору Цикл
		Стр.КлючСтроки = Новый УникальныйИдентификатор;	
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	тчКПодбору.Продукция_Элемент КАК Продукция_Элемент,
	|	тчКПодбору.КлючСтроки КАК КлючСтроки,
	|	тчКПодбору.Коэффициент КАК Коэффициент
	|ПОМЕСТИТЬ втТчКПодбору
	|ИЗ
	|	&тчКПодбору КАК тчКПодбору
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВСД_Партия.Ссылка КАК Партия,
	|	ВСД_Партия.ДатаИзготовления1 КАК ДатаИзготовления1,
	|	ВСД_Партия.ДатаИзготовления2 КАК ДатаИзготовления2,
	|	ВСД_Партия.ДатаСрокГодности1 КАК СрокГодности1,
	|	ВСД_Партия.ДатаСрокГодности2 КАК СрокГодности2,
	|	ВСД_Партия.Количество КАК Количество,
	|	втТчКПодбору.КлючСтроки КАК КлючСтроки,
	|	втТчКПодбору.Коэффициент КАК Коэффициент,
	|	ВСД_Партия.Количество / втТчКПодбору.Коэффициент КАК Упаковки
	|ИЗ
	|	втТчКПодбору КАК втТчКПодбору
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВСД_Партия КАК ВСД_Партия
	|		ПО втТчКПодбору.Продукция_Элемент = ВСД_Партия.Продукция_Элемент
	|ГДЕ
	|	НЕ ВСД_Партия.ПометкаУдаления
	|	И ВСД_Партия.Количество > 0
	|	И ВСД_Партия.Получатель_ХозСубъект = &Получатель_ХозСубъект
	|	И ВСД_Партия.Получатель_Площадка = &Получатель_Площадка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаИзготовления1";
	Запрос.УстановитьПараметр("тчКПодбору", тчКПодбору.Выгрузить());
	Запрос.УстановитьПараметр("Получатель_ХозСубъект", Отправитель_ХозСубъект);
	Запрос.УстановитьПараметр("Получатель_Площадка", Отправитель_Площадка);
	тзРезультат = Запрос.Выполнить().Выгрузить(); 
		
	Для Каждого Стр Из тзРезультат Цикл
		новСтрока = ПартииВНаличии.Добавить();
		ЗаполнитьЗначенияСвойств(новСтрока, Стр,,"ДатаИзготовления1, ДатаИзготовления2, СрокГодности1, СрокГодности2");
		новСтрока.ДатаИзготовления1 = ?(ЗначениеЗаполнено(Стр.ДатаИзготовления1), кб99_ВСД_Запросы.СтрокаВДату(Стр.ДатаИзготовления1), Неопределено);
		новСтрока.ДатаИзготовления2 = ?(ЗначениеЗаполнено(Стр.ДатаИзготовления2), кб99_ВСД_Запросы.СтрокаВДату(Стр.ДатаИзготовления2), Неопределено);
		новСтрока.СрокГодности1 = ?(ЗначениеЗаполнено(Стр.СрокГодности1), кб99_ВСД_Запросы.СтрокаВДату(Стр.СрокГодности1), Неопределено);
		новСтрока.СрокГодности2 = ?(ЗначениеЗаполнено(Стр.СрокГодности2), кб99_ВСД_Запросы.СтрокаВДату(Стр.СрокГодности2), Неопределено);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура тчКПодборуПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		ЭтаФорма.Элементы.ПартииВНаличии.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСтроки", "");
		ЭтаФорма.Элементы.ПодобранныеПартии.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСтроки", "");
		ИтогоВНаличии = 0;
		ИтогоОсталосьПодобрать = 0;
		ИтогоПодобрано = 0;
	Иначе
		ЭтаФорма.Элементы.ПартииВНаличии.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСтроки", Элемент.ТекущиеДанные.КлючСтроки);
		ЭтаФорма.Элементы.ПодобранныеПартии.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСтроки",Элемент.ТекущиеДанные.КлючСтроки);
		ОбновитьНадписиВПодвале(Элемент.ТекущиеДанные.КлючСтроки);
	КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ПриВводеЧисла(Результат, ТекущиеСтроки) Экспорт 
	
	Если Результат <> Неопределено  Тогда
		ТекущаяСтрокаТчКПодбору = ТекущиеСтроки.ТекущаяСтрокаТчКПодбору;
		ТекущаяСтрокаПартииВНаличии = ТекущиеСтроки.ТекущаяСтрокаПартииВНаличии;
		
		//Если Результат > ТекущаяСтрокаПартииВНаличии.Количество Тогда
		//	Результат = ТекущаяСтрокаПартииВНаличии.Количество;
		//ИначеЕсли Результат > ТекущаяСтрокаТчКПодбору.Количество Тогда
		//	Результат = ТекущаяСтрокаТчКПодбору.Количество;
		//КонецЕсли;
		Если Результат > ТекущаяСтрокаПартииВНаличии.Упаковки Тогда
			Результат = ТекущаяСтрокаПартииВНаличии.Упаковки;
		ИначеЕсли Результат > ТекущаяСтрокаТчКПодбору.Упаковки Тогда
			Результат = ТекущаяСтрокаТчКПодбору.Упаковки;
		КонецЕсли;
		
		//ТекущаяСтрокаПартииВНаличии.Количество = ТекущаяСтрокаПартииВНаличии.Количество - Результат;
		//ТекущаяСтрокаТчКПодбору.Количество = ТекущаяСтрокаТчКПодбору.Количество - Результат;
		
		КоличествоКг = Результат * ТекущаяСтрокаТчКПодбору.Коэффициент;
		ТекущаяСтрокаПартииВНаличии.Количество = ТекущаяСтрокаПартииВНаличии.Количество - КоличествоКг;
		ТекущаяСтрокаПартииВНаличии.Упаковки = ТекущаяСтрокаПартииВНаличии.Упаковки - Результат;
		
		ТекущаяСтрокаТчКПодбору.Количество = ТекущаяСтрокаТчКПодбору.Количество - КоличествоКг;
		ТекущаяСтрокаТчКПодбору.Упаковки = ТекущаяСтрокаТчКПодбору.Упаковки - Результат;
		
		СтруктураОтбора = Новый Структура("Партия", ТекущаяСтрокаПартииВНаличии.Партия);
		стрПодобранныеПартии = ПодобранныеПартии.НайтиСтроки(СтруктураОтбора);
		Если стрПодобранныеПартии.Количество() > 0 Тогда
			//стрПодобранныеПартии[0].Количество = стрПодобранныеПартии[0].Количество + Результат;	
			стрПодобранныеПартии[0].Количество = стрПодобранныеПартии[0].Количество + КоличествоКг;
			стрПодобранныеПартии[0].Упаковки = стрПодобранныеПартии[0].Упаковки + Результат;
		Иначе
			//стрПодобранныеПартии = ПодобранныеПартии.Добавить();
			//ЗаполнитьЗначенияСвойств(стрПодобранныеПартии, ТекущаяСтрокаПартииВНаличии,,"Количество");
			//стрПодобранныеПартии.Количество = Результат;
			стрПодобранныеПартии = ПодобранныеПартии.Добавить();
			ЗаполнитьЗначенияСвойств(стрПодобранныеПартии, ТекущаяСтрокаПартииВНаличии,,"Количество, Упаковки");
			стрПодобранныеПартии.Количество = КоличествоКг;
			стрПодобранныеПартии.Упаковки = Результат;
		КонецЕсли;
		ОбновитьНадписиВПодвале(ТекущаяСтрокаТчКПодбору.КлючСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПартииВНаличииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущаяСтрокаТчКПодбору = Элементы.тчКПодбору.ТекущиеДанные;
	ТекущаяСтрока = Элемент.ТекущиеДанные;
	
	Если ТекущаяСтрокаТчКПодбору.Количество > 0 
				 И ТекущаяСтрока.Количество > 0 Тогда
		ТекущиеСтроки = Новый Структура();
		ТекущиеСтроки.Вставить("ТекущаяСтрокаТчКПодбору", ТекущаяСтрокаТчКПодбору);
		ТекущиеСтроки.Вставить("ТекущаяСтрокаПартииВНаличии", ТекущаяСтрока);
		ОповещениеОВводеЧисла = Новый ОписаниеОповещения("ПриВводеЧисла", ЭтаФорма,	ТекущиеСтроки);
		Подсказка = "Введите количество";
		//ПредложенноеЧисло = Мин(ТекущаяСтрока.Количество, ТекущаяСтрокаТчКПодбору.Количество);
		ПредложенноеЧисло = Мин(ТекущаяСтрока.Упаковки, ТекущаяСтрокаТчКПодбору.Упаковки);
		ПоказатьВводЧисла(ОповещениеОВводеЧисла, ПредложенноеЧисло, Подсказка, 15, 3);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПодобранныеПартииПередУдалением(Элемент, Отказ)
	
	текСтрока = Элемент.ТекущиеДанные;
	СтруктураДляПоиска = Новый Структура("КлючСтроки", текСтрока.КлючСтроки); 
	
	СтрокаТчКПодбору = тчКПодбору.НайтиСтроки(СтруктураДляПоиска); 
	Для Каждого Строка Из СтрокаТчКПодбору Цикл 
		Строка.Количество = Строка.Количество + текСтрока.Количество;
		Строка.Упаковки = Строка.Упаковки + текСтрока.Упаковки;
	КонецЦикла;
	
	СтруктураДляПоиска = Новый Структура("Партия", текСтрока.Партия); 
	СтрокаПартии = ПартииВНаличии.НайтиСтроки(СтруктураДляПоиска); 
	Для Каждого Строка Из СтрокаПартии Цикл 
		Строка.Количество = Строка.Количество + текСтрока.Количество;
		Строка.Упаковки = Строка.Упаковки + текСтрока.Упаковки;
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьПодбор(Команда)
	
	НеПодобранныеТовары = Новый Массив;
	Для Каждого Стр Из тчКПодбору Цикл
		Если Стр.Количество > 0 Тогда
			СтрСтруктура = Новый Структура;
			СтрСтруктура.Вставить("Номенклатура", Стр.Номенклатура);
			СтрСтруктура.Вставить("Продукция_Элемент", Стр.Продукция_Элемент);
			СтрСтруктура.Вставить("Количество", Стр.Количество);
			СтрСтруктура.Вставить("Упаковки", Стр.Упаковки);
			НеПодобранныеТовары.Добавить(СтрСтруктура);
		КонецЕсли;
	КонецЦикла;
	
	ПодобранныеТовары = Новый Массив;
	Для Каждого Стр Из ПодобранныеПартии Цикл
		ОтборТчКПодбору = Новый Структура("КлючСтроки", Стр.КлючСтроки);
		СтрокаТчКПодбору = тчКПодбору.НайтиСтроки(ОтборТчКПодбору);
		СтрСтруктура = Новый Структура;
		Если СтрокаТчКПодбору.Количество() > 0 Тогда
			СтрСтруктура.Вставить("Номенклатура", СтрокаТчКПодбору[0].Номенклатура);
			СтрСтруктура.Вставить("Продукция_Элемент", СтрокаТчКПодбору[0].Продукция_Элемент);
		КонецЕсли;
		СтрСтруктура.Вставить("Партия", Стр.Партия);
		СтрСтруктура.Вставить("Количество", Стр.Количество);
		СтрСтруктура.Вставить("Упаковки", Стр.Упаковки);
		ПодобранныеТовары.Добавить(СтрСтруктура);
	КонецЦикла;

	РезультатПодбора = Новый Структура;
	РезультатПодбора.Вставить("НеПодобранныеТовары", НеПодобранныеТовары);
	РезультатПодбора.Вставить("ПодобранныеТовары", ПодобранныеТовары);
	
	Закрыть(РезультатПодбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНадписиВПодвале(КлючСтроки = Неопределено)
	
	Если ИтогиПоПартиям Тогда
		текСтрПодобранныеПартии = Элементы.ПодобранныеПартии.ТекущиеДанные;
		//ИтогоВНаличии = текСтрПодобранныеПартии.Количество;
		ИтогоВНаличии = текСтрПодобранныеПартии.Упаковки;
		ИтогоОсталосьПодобрать = 0;
		//ИтогоПодобрано = текСтрПодобранныеПартии.Количество;
		ИтогоПодобрано = текСтрПодобранныеПартии.Упаковки;
	Иначе
		Если КлючСтроки = Неопределено И Элементы.тчКПодбору.ТекущиеДанные <> Неопределено Тогда
			КлючСтроки = Элементы.тчКПодбору.ТекущиеДанные.КлючСтроки;
		ИначеЕсли Элементы.тчКПодбору.ТекущиеДанные = Неопределено Тогда
			ИтогоВНаличии = 0;
			ИтогоОсталосьПодобрать = 0;
			ИтогоПодобрано = 0;
			Возврат;
		КонецЕсли;
		СтруктураОтбора = Новый Структура("КлючСтроки", КлючСтроки);
		стрПодобранныеПартии = ПодобранныеПартии.НайтиСтроки(СтруктураОтбора);
		стрПартииВНаличии = ПартииВНаличии.НайтиСтроки(СтруктураОтбора);
		стрТчКПодбору = тчКПодбору.НайтиСтроки(СтруктураОтбора);
		ИтогоВНаличии = 0;
		ИтогоОсталосьПодобрать = 0;
		ИтогоПодобрано = 0;
			
		Для Каждого элПартииВНаличии Из стрПартииВНаличии Цикл
			//ИтогоВНаличии = ИтогоВНаличии + элПартииВНаличии.Количество;
			ИтогоВНаличии = ИтогоВНаличии + элПартииВНаличии.Упаковки;
		КонецЦикла;
		
		Для Каждого элПодобранныеПартии Из стрПодобранныеПартии Цикл
			//ИтогоПодобрано = ИтогоПодобрано + элПодобранныеПартии.Количество;
			ИтогоПодобрано = ИтогоПодобрано + элПодобранныеПартии.Упаковки;
		КонецЦикла;
		
		Для Каждого элКПодбору Из стрТчКПодбору Цикл
			//ИтогоОсталосьПодобрать = элКПодбору.Количество;
			ИтогоОсталосьПодобрать = элКПодбору.Упаковки;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобранныеПартииПослеУдаления(Элемент)
	
	ОбновитьНадписиВПодвале();
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПодбор(Команда)

	тчКПодбору.Очистить();
	Элементы.тчКПодбору.Доступность = Истина;
	Элементы.тчКПодбору.ТолькоПросмотр = Ложь;

	ПартииВНаличии.Очистить();
	Элементы.ПартииВНаличии.Доступность = Истина;
	Элементы.ПартииВНаличии.ТолькоПросмотр = Ложь;
	
	ПодобранныеПартии.Очистить();
	ИтогиПоПартиям = Ложь;
	ЗаполнитьПоДокументуОснованию();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВсеПартииПоПлощадке(Команда)
	
	ТекстВопроса = "Табличная часть документа будет перезаполнена, продолжить?";
	Оповещение = Новый ОписаниеОповещения("ВыбратьВсеПартииОтвет", ЭтаФорма);	
    ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,  0, КодВозвратаДиалога.Да, "");    

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсеПартииОтвет(Ответ, Парам) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОповещениеОВводеЧисла = Новый ОписаниеОповещения("ПослеВводаКоличествоПартий", ЭтаФорма);
		Подсказка = "Введите количество партий для заполнения";
		ПоказатьВводЧисла(ОповещениеОВводеЧисла, 500, Подсказка, 4, 0);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаКоличествоПартий(Результат, Парам = Неопределено) Экспорт 
	
	Если Результат <> Неопределено  Тогда
    	ЗаполнитьПартиямиПоПлощадке(Результат);
		тчКПодборуПриАктивизацииСтроки(Элементы.тчКПодбору)	
	КонецЕсли;
	
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьПартиямиПоПлощадке(КоличествоПартий);

	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ %КоличествоПартий%
	|	ВСД_Партия.Ссылка КАК Партия,
	|	ВСД_Партия.ДатаИзготовления1 КАК ДатаИзготовления1,
	|	ВСД_Партия.ДатаИзготовления2 КАК ДатаИзготовления2,
	|	ВСД_Партия.ДатаСрокГодности1 КАК СрокГодности1,
	|	ВСД_Партия.ДатаСрокГодности2 КАК СрокГодности2,
	|	0 КАК КлючСтроки,
	|	ВСД_Партия.Количество КАК Количество
	|ИЗ
	|	Справочник.ВСД_Партия КАК ВСД_Партия
	|ГДЕ
	|	НЕ ВСД_Партия.ПометкаУдаления
	|	И ВСД_Партия.Получатель_ХозСубъект = &Получатель_ХозСубъект
	|	И ВСД_Партия.Получатель_Площадка = &Получатель_Площадка
	|	И ВСД_Партия.Количество > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Партия";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%КоличествоПартий%", Строка(КоличествоПартий));
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Получатель_ХозСубъект", Отправитель_Хозсубъект);
	Запрос.УстановитьПараметр("Получатель_Площадка", Отправитель_Площадка);
	
	тчКПодбору.Очистить();
	Элементы.тчКПодбору.Доступность = Ложь;
	Элементы.тчКПодбору.ТолькоПросмотр = Истина;
	
	ПартииВНаличии.Очистить();
	Элементы.ПартииВНаличии.Доступность = Ложь;
	Элементы.ПартииВНаличии.ТолькоПросмотр = Истина;
	
	ПодобранныеПартии.Очистить();
	ПодобранныеПартии.Загрузить(Запрос.Выполнить().Выгрузить());
	
	ИтогиПоПартиям = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобранныеПартииПриАктивизацииСтроки(Элемент)
	
	ОбновитьНадписиВПодвале()
	
КонецПроцедуры
