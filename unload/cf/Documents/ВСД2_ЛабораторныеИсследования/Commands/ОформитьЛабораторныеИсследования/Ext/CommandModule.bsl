
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	ОформленныеИсследования = ОформитьЛабИсследования(ПараметрКоманды);
	Для Каждого Док Из ОформленныеИсследования Цикл
		ПараметрыФормы = Новый Структура("Ключ", Док);
		ОткрытьФорму("Документ.ВСД2_ЛабораторныеИсследования.Форма.ФормаДокумента", ПараметрыФормы);
	КонецЦикла;

КонецПроцедуры


&НаСервере
Функция ОформитьЛабИсследования(ДокОснование)
	
	ПараметрыОрганизации = кб99_ВСД.ЗагрузитьПараметры(ДокОснование.Организация);
	Если Не ЗначениеЗаполнено(ПараметрыОрганизации.param_vetdoctor_login) Тогда
		кб99_ВСД.СообщитьИнфо("У вас не достаточно прав для оформления лабораторных исследований");
		Возврат Новый Массив;
	КонецЕсли;

	мсДокументов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокОснование", ДокОснование);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВСД_Партия.ДатаИзготовления1 КАК ДатаИзготовления1,
	|	ВСД_Партия.ВсдДата КАК ВсдДата,
	|	ВСД_Партия.Количество КАК Количество,
	|	ВСД_Партия.Ссылка КАК Партия,
	|	ВСД_Партия.Представление КАК ПартияПредставление,
	|	ВСД_Партия.Продукция_Элемент КАК Продукция,
	|	ВСД_Партия.НомерЗаписи КАК НомерЗаписи,
	|	ВСД_Партия.Статус КАК СтатусПартии,
	|	ВСД2_ЛабораторныеИсследования.Ссылка КАК ЛабИсследования
	|ИЗ
	|	Справочник.кб99_ВСД2 КАК кб99_ВСД2
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВСД_Партия КАК ВСД_Партия
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВСД2_ЛабораторныеИсследования КАК ВСД2_ЛабораторныеИсследования
	|			ПО (ВСД2_ЛабораторныеИсследования.Партия = ВСД_Партия.Ссылка)
	|			И (ВСД2_ЛабораторныеИсследования.ПометкаУдаления = ЛОЖЬ)
	|		ПО кб99_ВСД2.Ссылка = ВСД_Партия.vetDocument
	|ГДЕ
	|	кб99_ВСД2.ДокументОснование = &ДокОснование";
	ВыборкаПартии = Запрос.Выполнить().Выбрать();

	Пока ВыборкаПартии.Следующий() Цикл
		
		Если НЕ СокрЛП(ВыборкаПартии.СтатусПартии) = "103" 
					И НЕ СокрЛП(ВыборкаПартии.СтатусПартии) = "102" Тогда
					
			кб99_ВСД.СообщитьИнфо(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Партия %1 была изменена. Оформление лабораторных исследований невозможно",
					ВыборкаПартии.ПартияПредставление));
			Продолжить;
		КонецЕсли;
		
		ПараметрыДокумента = Новый Структура();
		ПараметрыДокумента.Вставить("Организация", ДокОснование.Организация);
		ПараметрыДокумента.Вставить("ДокументОснование", ДокОснование);
		ПараметрыДокумента.Вставить("Партия", ВыборкаПартии.Партия);
		ПараметрыДокумента.Вставить("Продукция", ВыборкаПартии.Продукция);
		
		Если ЗначениеЗаполнено(ВыборкаПартии.ЛабИсследования) Тогда
			НовыйДокумент = ВыборкаПартии.ЛабИсследования.ПолучитьОбъект();
		Иначе
			НовыйДокумент = Документы.ВСД2_ЛабораторныеИсследования.СоздатьДокумент();
		КонецЕсли;
        	
		НовыйДокумент.Заполнить(ПараметрыДокумента);
		НовыйДокумент.Записать();
		
		мсДокументов.Добавить(НовыйДокумент.Ссылка);
		
	КонецЦикла;
	
	Возврат мсДокументов;
	
КонецФункции