&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	кб99_ВСД_Общий.ПроверитьВозможностьИспользованияОтчета();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	ТабДок = СформироватьОтчетНаСервере();
	
	ПараметрыОткрытияФормы = Новый Структура("ТабДок", ТабДок);
	ОткрытьФорму("Отчет.кб99_ОстаткиПартий.Форма.ФормаТабличногоДокумента", ПараметрыОткрытияФормы);
	//ТабДок.ОтображатьСетку = Ложь;
	////ТабДок.Автомасштаб = Истина;
	//ТабДок.ОтображатьЗаголовки = Ложь;	
	//ТабДок.Показать("Заявка поставщику");  
	
КонецПроцедуры 

&НаСервере
Функция СформироватьОтчетНаСервере()
	
	ТабДок = Новый ТабличныйДокумент;
	Обработка =  РеквизитФормыВЗначение("Объект");
	Макет = Обработка.ПолучитьМакет("МакетОтчета");
	
	ОбластьЗаголовок	  = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапкаТаблицы1с = Макет.ПолучитьОбласть("ШапкаТаблицыОстатки1с");
	ОбластьСклад		  = Макет.ПолучитьОбласть("Склад");
	ОбластьНоменклатура1с = Макет.ПолучитьОбласть("стрТовары1с");
	ОбластьИтог1с		  = Макет.ПолучитьОбласть("Итог1с");
	
	ОбластьЗаголовок.Параметры.ВыбПродукцияЭлемент = выбПродукцияЭлемент.Наименование;
	ОбластьЗаголовок.Параметры.ВыбПлощадка		   = выбПлощадка;
	ОбластьЗаголовок.Параметры.ВыбДата			   = Формат(выбДата, "ДЛФ=D");
	ТабДок.Вывести(ОбластьЗаголовок);
	
	ТабДок.Вывести(ОбластьШапкаТаблицы1с);

	ТабДок.НачатьАвтогруппировкуСтрок();
		
	Запрос = Новый Запрос;
	Запрос.Текст = "
		|ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Склад КАК Склад,
		|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыНаСкладахОстатки.ВНаличииОстаток КАК ОстатокШт,
		|	ТоварыНаСкладахОстатки.ВНаличииОстаток * ТоварыНаСкладахОстатки.Номенклатура.ВесЧислитель / ТоварыНаСкладахОстатки.Номенклатура.ВесЗнаменатель КАК ОстатокВес
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(
		|			&Дата,
		|			Склад В
		|					(ВЫБРАТЬ
		|						ВСД_ПлощадкаСклады.Склад КАК Склад
		|					ИЗ
		|						Справочник.ВСД_Площадка.Склады КАК ВСД_ПлощадкаСклады
		|					ГДЕ
		|						ВСД_ПлощадкаСклады.Ссылка = &выбПлощадка)
		|				И Номенклатура В
		|					(ВЫБРАТЬ
		|						ВСД_Соответсвия.Владелец КАК Номенклатура
		|					ИЗ
		|						Справочник.ВСД_Соответсвия КАК ВСД_Соответсвия
		|					ГДЕ
		|						ВСД_Соответсвия.ПродукцияЭлемент = &выбПродукцияЭлемент)) КАК ТоварыНаСкладахОстатки
		|ИТОГИ
		|	СУММА(ОстатокШт),
		|	СУММА(ОстатокВес)
		|ПО
		|	Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	кб99_ОстаткиПартийВетисСрезПоследних.Партия.Продукция_Элемент КАК Продукция_Элемент,
		|	кб99_ОстаткиПартийВетисСрезПоследних.Партия.ДатаИзготовления1 КАК ДатаИзготовления,
		|	кб99_ОстаткиПартийВетисСрезПоследних.Партия.ДатаСрокГодности1 КАК СрокГодности,
		|	кб99_ОстаткиПартийВетисСрезПоследних.Партия КАК Партия,
		|	кб99_ОстаткиПартийВетисСрезПоследних.Количество КАК Остаток
		|ИЗ
		|	РегистрСведений.кб99_ОстаткиПартийВетис.СрезПоследних(
		|			&Дата,
		|			Партия.Получатель_Площадка = &выбПлощадка
		|				И Партия.Продукция_Элемент = &выбПродукцияЭлемент) КАК кб99_ОстаткиПартийВетисСрезПоследних
		|ГДЕ
		|	кб99_ОстаткиПартийВетисСрезПоследних.Партия.Получатель_Площадка = &выбПлощадка
		|ИТОГИ
		|	СУММА(Количество)
		|ПО
		|	Продукция_Элемент";		
	Запрос.УстановитьПараметр("Дата", КонецДня(выбДата));
	Запрос.УстановитьПараметр("выбПлощадка", выбПлощадка);
	Запрос.УстановитьПараметр("выбПродукцияЭлемент", выбПродукцияЭлемент);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаПоСкладам = РезультатЗапроса[0].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Итог1сШт  = 0;
	Итог1сВес = 0;
	
	Пока ВыборкаПоСкладам.Следующий() Цикл
		
		ОбластьСклад.Параметры.Заполнить(ВыборкаПоСкладам);
		ТабДок.Вывести(ОбластьСклад, 1); 
		
		Итог1сВес = Итог1сВес + ВыборкаПоСкладам.ОстатокВес;
		Итог1сШт  = Итог1сШт  + ВыборкаПоСкладам.ОстатокШт;
		
		ВыборкаОстатки = ВыборкаПоСкладам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаОстатки.Следующий() Цикл
			
			ОбластьНоменклатура1с.Параметры.Заполнить(ВыборкаОстатки);
			ТабДок.Вывести(ОбластьНоменклатура1с, 2); 
			
		КонецЦикла;
		
	КонецЦикла;	
	
	ОбластьИтог1с.Параметры.Итог1сШт  = Итог1сШт;
	ОбластьИтог1с.Параметры.Итог1сВес = Итог1сВес;
	ТабДок.Вывести(ОбластьИтог1с, 1); 
	
	#Область ВыводостатковПоМеркурию
	
	ОбластьШапкаТаблицыОстаткиМерк	 = Макет.ПолучитьОбласть("ШапкаТаблицыОстаткиМерк");
	ОбластьСтрПродукция				 = Макет.ПолучитьОбласть("стрПродукция");
	ОбластьСтрПартия				 = Макет.ПолучитьОбласть("стрПартия");
	ОбластьИтогПоПартиям	         = Макет.ПолучитьОбласть("ИтогПоПартиям");
	
	ТабДок.Вывести(ОбластьШапкаТаблицыОстаткиМерк);
	
	ВыборкаПоПродукции = РезультатЗапроса[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ИтогПоПартиям = 0;
	
	Пока ВыборкаПоПродукции.Следующий() Цикл
		
		ОбластьСтрПродукция.Параметры.Заполнить(ВыборкаПоПродукции);
		ТабДок.Вывести(ОбластьСтрПродукция, 1); 
		
		ИтогПоПартиям = ИтогПоПартиям + ВыборкаПоПродукции.Остаток; 
		
		ВыборкаПартии = ВыборкаПоПродукции.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПартии.Следующий() Цикл
			
			ОбластьСтрПартия.Параметры.Заполнить(ВыборкаПартии);
			ОбластьСтрПартия.Параметры.ДатаИзготовления = Формат(кб99_ВСД_Запросы.СтрокаВДату(ВыборкаПартии.ДатаИзготовления), "ДЛФ=D");
			ОбластьСтрПартия.Параметры.СрокГодности = Формат(кб99_ВСД_Запросы.СтрокаВДату(ВыборкаПартии.СрокГодности), "ДЛФ=D");
			
			СтруктураРасшифровки = Новый Структура;
			СтруктураРасшифровки.Вставить("Партия", ВыборкаПартии.Партия);
            ОбластьСтрПартия.Параметры.РасшифровкаПартии = СтруктураРасшифровки;
			
			ТабДок.Вывести(ОбластьСтрПартия, 2); 
			
		КонецЦикла;
		
	КонецЦикла;
	
	ОбластьИтогПоПартиям.Параметры.ИтогПоПартиям = ИтогПоПартиям;
	ТабДок.Вывести(ОбластьИтогПоПартиям, 1);
	
	#КонецОбласти
	
	ТабДок.ЗакончитьАвтогруппировкуСтрок();
	ТабДок.ПоказатьУровеньГруппировокСтрок(0);
	
	Возврат ТабДок;
	
КонецФункции
