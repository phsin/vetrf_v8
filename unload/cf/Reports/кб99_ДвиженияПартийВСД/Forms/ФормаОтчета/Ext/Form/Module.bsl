
&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбработкаРасшифровки = Новый ОбработкаРасшифровкиКомпоновкиДанных(ДанныеРасшифровки, Новый ИсточникДоступныхНастроекКомпоновкиДанных(Отчет));
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Расшифровка", Расшифровка);
	ДополнительныеПараметры.Вставить("ОтчетДанныеРасшифровки", ДанныеРасшифровки);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПоказатьВыборДействияЗавершение", ЭтаФорма, ДополнительныеПараметры);
	
	ДоступныеДействия = Новый Массив;
	ДоступныеДействия.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение);
	
	ДополнительныеПунктыМеню = Новый СписокЗначений;
	ДополнительныеПунктыМеню.Добавить("ДокументыДвижения", НСтр("ru = 'Документы движения'"));
	
	ОбработкаРасшифровки.ПоказатьВыборДействия(ОписаниеОповещения, Расшифровка, ДоступныеДействия, ДополнительныеПунктыМеню);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВыборДействияЗавершение(ВыполненноеДействие, ПараметрВыполненногоДействия, ДополнительныеПараметры) Экспорт
	
	Если ВыполненноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение Тогда
		
		ПоказатьЗначение(Неопределено, ПараметрВыполненногоДействия);
		
	ИначеЕсли ВыполненноеДействие = "ДокументыДвижения" Тогда
		
		ВыбранноеПоле = ПолеРасшифровкиОтчетаДвижениеМаркированногоТовара(ДополнительныеПараметры.ОтчетДанныеРасшифровки, ДополнительныеПараметры.Расшифровка);
		
		Если ВыбранноеПоле = Неопределено Тогда
			Возврат
		КонецЕсли;
		
		ОткрытьОтчетРасшифровкуПоВыбранномуПолю(ВыбранноеПоле);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолеРасшифровкиОтчетаДвижениеМаркированногоТовара(ОтчетДанныеРасшифровки, Расшифровка)
	
	ПоляРасшифровки = ПолучитьИзВременногоХранилища(ДанныеРасшифровки).Элементы.Получить(Расшифровка).ПолучитьПоля();
	
	НайденноеПоле = ПоляРасшифровки.Найти("Партия");
	Если НайденноеПоле = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат НайденноеПоле.Значение;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьОтчетРасшифровкуПоВыбранномуПолю(ВыбранноеПоле)
	
	Отбор = Новый Структура;
	Отбор.Вставить("Партия", ВыбранноеПоле);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Отбор);
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", "Партия");
	
	ОткрытьФорму("Отчет.кб99_ДвиженияПартийВСД.Форма.ФормаРасшифровки", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор);
	
КонецПроцедуры