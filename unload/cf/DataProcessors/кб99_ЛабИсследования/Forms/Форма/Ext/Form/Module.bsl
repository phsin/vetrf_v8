&НаКлиенте
Перем ФормаДлительнойОперации Экспорт;

&НаКлиенте
Перем ПараметрыОбработчикаОжидания Экспорт;

&НаКлиенте
Перем АдресПараметров Экспорт;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Док") Тогда
		ДокОснование = Параметры.Док;
		Если ТипЗнч(ДокОснование) = Тип("Массив") Тогда
			СписокДокументов.ЗагрузитьЗначения(ДокОснование);
		Иначе
			СписокДокументов.Добавить(ДокОснование);
		КонецЕсли;
		
		Объект.Организация = СписокДокументов[0].Значение.Организация;	
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДокОснование", СписокДокументов.ВыгрузитьЗначения());
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВСД_Партия.ДатаИзготовления1 КАК ДатаИзготовления1,
		|	ВСД_Партия.ВсдДата КАК ВсдДата,
		|	ВСД_Партия.Количество КАК Количество,
		|	ВСД_Партия.Ссылка КАК Партия,
		|	ВСД_Партия.Продукция_Элемент КАК Продукция,
		|	ВСД_Партия.НомерЗаписи КАК НомерЗаписи,
		|	ВСД_Партия.Статус КАК СтатусПартии,
		|	ВСД2_ЛабораторныеИсследования.Ссылка КАК ЛабИсследования,
		|	кб99_ЗапросыСрезПоследних.СтатусЗапроса КАК СтатусЗапроса,
		|	кб99_ЗапросыСрезПоследних.ApplicationID КАК ApplicationID,
		|	кб99_ЗапросыСрезПоследних.Ошибки КАК Ошибки
		|ИЗ
		|	Справочник.кб99_ВСД2 КАК кб99_ВСД2
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВСД_Партия КАК ВСД_Партия
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВСД2_ЛабораторныеИсследования КАК ВСД2_ЛабораторныеИсследования
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.кб99_Запросы.СрезПоследних КАК кб99_ЗапросыСрезПоследних
		|				ПО ВСД2_ЛабораторныеИсследования.Ссылка = кб99_ЗапросыСрезПоследних.Объект
		|			ПО ВСД2_ЛабораторныеИсследования.Партия = ВСД_Партия.Ссылка
		|				И (ВСД2_ЛабораторныеИсследования.ПометкаУдаления = ЛОЖЬ)
		|		ПО кб99_ВСД2.Ссылка = ВСД_Партия.vetDocument
		|ГДЕ
		|	ВСД_Партия.ДокОснование В (&ДокОснование)";
		Рез = Запрос.Выполнить();
		
		Объект.Партии.Загрузить(Рез.Выгрузить());
		Объект.ВыбДокПроизводства = СписокДокументов[0].Значение;
		
	Иначе
		Объект.Организация = кб99_ВСД_Общий.ПолучитьОрганизациюПоУмолчанию();
		ЗаполнитьТабличныеЧасти();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПараметрыОрганизации = кб99_ВСД.ЗагрузитьПараметры(Объект.Организация);
	Если Не ЗначениеЗаполнено(ПараметрыОрганизации.param_vetdoctor_login) Тогда
		кб99_ВСД.СообщитьИнфо("У вас не достаточно прав для оформления лабораторных исследований");
		Отказ = Истина;
	ИначеЕсли ЗначениеЗаполнено(Объект.ВыбДокПроизводства) И Не ЗначениеЗаполнено(Объект.Партии) Тогда
		кб99_ВСД.СообщитьИнфо(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("По документу %1 не требуется оформление лабораторных исследований."+	
			Символы.ПС + "Возможно они оформлены ранее.", 
				Объект.ВыбДокПроизводства));
		Отказ = Истина;
	КонецЕсли;
 
КонецПроцедуры

&НаСервере
Функция ПолучитьСообщенияНаСервере( ) 
    
	Попытка		
		Сообщения = ДлительныеОперации.СообщенияПользователю( Истина, ИдентификаторЗадания );
	Исключение
		// если в ОбщемМодуле нет функции 
		// например в Бухгалтерии 2.0
		Сообщения = кб99_ВСД_Общий.СообщенияПользователю( Истина, ИдентификаторЗадания );
	КонецПопытки;
	
 	Возврат Сообщения;
    
КонецФункции

&НаКлиенте
Процедура ЗакрытьФормуДлительнойОперации()
	
	ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации( ФормаДлительнойОперации );
	
КонецПроцедуры

&НаСервере
Функция ЗаданиеВыполнено() 
	Возврат ДлительныеОперации.ЗаданиеВыполнено( ИдентификаторЗадания );
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()
	
	МассивСообщений = ПолучитьСообщенияНаСервере( );
	Для Каждого Сообщение Из МассивСообщений Цикл
		Сообщение.Сообщить();
		Сообщение.ИдентификаторНазначения = УникальныйИдентификатор;
		Сообщение.Сообщить();
	КонецЦикла;
	
	Попытка
		Если ЗаданиеВыполнено() Тогда 
			ЗакрытьФормуДлительнойОперации();
			Возврат;
		КонецЕсли;		
	Исключение
		ЗакрытьФормуДлительнойОперации();
		ВызватьИсключение;
	КонецПопытки;
 
	ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.ТекущийИнтервал * ПараметрыОбработчикаОжидания.КоэффициентУвеличенияИнтервала;
	Если ПараметрыОбработчикаОжидания.ТекущийИнтервал > ПараметрыОбработчикаОжидания.МаксимальныйИнтервал Тогда
		ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.МаксимальныйИнтервал;
	КонецЕсли;
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличныеЧасти()
	
    Запрос = Новый Запрос;	
    Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;

    Запрос.Текст =
    "ВЫБРАТЬ
    |	ВСД_Партия.Ссылка КАК Партия,
    |	ВСД_Партия.ВсдДата КАК ВсдДата,
    |	ВСД_Партия.ДатаИзготовления1 КАК ДатаИзготовления1,
    |	ВСД_Партия.Продукция_Элемент КАК Продукция,
    |	ВСД_Партия.НомерЗаписи КАК НомерЗаписи,
    |	ВСД_Партия.Статус КАК СтатусПартии,
    |	ВСД_Партия.Количество КАК Количество,
    |	ВСД2_ЛабораторныеИсследования.Ссылка КАК ЛабИсследования,
    |	кб99_ЗапросыСрезПоследних.СтатусЗапроса КАК СтатусЗапроса,
    |	кб99_ЗапросыСрезПоследних.ApplicationID КАК ApplicationID,
    |	кб99_ЗапросыСрезПоследних.Ошибки КАК Ошибки
    |ИЗ
    |	Справочник.ВСД_Партия КАК ВСД_Партия
    |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВСД2_ЛабораторныеИсследования КАК ВСД2_ЛабораторныеИсследования
    |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.кб99_Запросы.СрезПоследних КАК кб99_ЗапросыСрезПоследних
    |			ПО ВСД2_ЛабораторныеИсследования.Ссылка = кб99_ЗапросыСрезПоследних.Объект
    |		ПО (ВСД2_ЛабораторныеИсследования.Партия = ВСД_Партия.Ссылка)
    |			И (ВСД2_ЛабораторныеИсследования.ПометкаУдаления = ЛОЖЬ)
    |ГДЕ
    |	ВСД_Партия.ПометкаУдаления = ЛОЖЬ";

	Если ЗначениеЗаполнено( Объект.ДатаСоздания	) Тогда 
		Запрос.Текст = Запрос.Текст + "
		| И ВСД_Партия.ВсдДата = &ДатаСоздания";
		Запрос.УстановитьПараметр("ДатаСоздания", Объект.ДатаСоздания );
	КонецЕсли;
	Если ЗначениеЗаполнено( Объект.ВыбПродукция	) Тогда 
		Запрос.Текст = Запрос.Текст + "
		| И ВСД_Партия.Продукция_Элемент = &Продукция_Элемент";
		Запрос.УстановитьПараметр("Продукция_Элемент", Объект.ВыбПродукция );
	КонецЕсли;
	Если ЗначениеЗаполнено( Объект.ВыбДокПроизводства ) Тогда
		Запрос.Текст = Запрос.Текст + "
		| И ВСД_Партия.ДокОснование В (&ДокОснование)";
		Запрос.УстановитьПараметр("ДокОснование", СписокДокументов.ВыгрузитьЗначения());
	КонецЕсли;

    Попытка
        РезультатЗапроса = Запрос.Выполнить();
		
		Объект.Партии.Загрузить( РезультатЗапроса.Выгрузить());
    Исключение
        РезультатЗапроса = Неопределено;
        кб99_ВСД.СообщитьИнфо("Указан неверный параметр "+Описаниеошибки());
    КонецПопытки;

КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьТабличныеЧасти();
КонецПроцедуры

&НаСервере
Процедура СоздатьЛИНаСервере()
		
	фОбъект = РеквизитФормыВЗначение("Объект");
	фОбъект.СоздатьЛИ();
	ЗначениеВРЕквизитФормы(фОбъект, "Объект");
		
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЛИ(Команда)
	СоздатьЛИНаСервере();
КонецПроцедуры

&НаСервере
Функция ПодготовитьСписокВСДКОтправке()
	
	сзДокументыКОтправке = новый Массив;
	Для Каждого СтрПартии Из Объект.Партии Цикл
		
		Если НЕ(ЗначениеЗаполнено(СтрПартии.ЛабИсследования)) или НЕ( СтрПартии.Пометка) Тогда 		
			Продолжить;    
		КонецЕсли;
		
		Если ( СтрПартии.СтатусЗапроса = "COMPLETED") или ( СтрПартии.ЛабИсследования.ПометкаУдаления ) Тогда
			Продолжить;
		КонецЕсли;
		
		сзДокументыКОтправке.Добавить( СтрПартии.ЛабИсследования );
	КонецЦикла;
	Возврат сзДокументыКОтправке;
	
КонецФункции

&НаСервере
Функция ОтправитьВСД_НаСервере( )
	
	сзДокументыКОтправке = ПодготовитьСписокВСДКОтправке( );	
	
	ПараметрыОрганизации = кб99_ВСД.ЗагрузитьПараметры( Объект.Организация );
	ПараметрыОрганизации.Вставить("СписокДокументов", сзДокументыКОтправке);
	
	ПараметрыФункции = Новый Структура;
	ПараметрыФункции.Вставить("Параметры", ПараметрыОрганизации);	
	
	Если Объект.ОтправлятьВФоне Тогда 
		ИдентификаторЗадания = Неопределено;		
		НаименованиеЗадания = НСтр("ru = 'Ветис отправка ВСД'");
		Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне( УникальныйИдентификатор, 
			"кб99_ВСД.ОтправитьВСДвГИС",
			ПараметрыФункции,
			НаименованиеЗадания);
		
		// результат обработки
		АдресХранилища       = Результат.АдресХранилища;		
		// для получения сообщений
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
	Иначе;
		Для Каждого ДокСсылка Из сзДокументыКОтправке Цикл
			кб99_ВСД.ОтправитьВСДвГИС(ДокСсылка);
		КонецЦикла;
		Результат = Новый Структура;
		Результат.Вставить("ЗаданиеВыполнено", Истина);
	КонецЕсли; 

	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура Отправить(Команда)	
	
	Состояние("Выполняется отправка ВСД",,"Ожидайте...",БиблиотекаКартинок.kb99_wrench);
	
	Результат = ОтправитьВСД_НаСервере( );
	
	Если Результат.ЗаданиеВыполнено Тогда
		// Задание отработало, результат получен
		ПоказатьОповещениеПользователя("Выполнено");
	ИначеЕсли ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);
	КонецЕсли;			
	
	ЗаполнитьТабличныеЧасти();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьЛабИсследПоСырьюНаСервере()
	
	запрос=новый запрос;
	запрос.Текст= "ВЫБРАТЬ
	              |	ВСД2_ПроизводствоПартииСписания.Партия КАК ПартияСписания,
	              |	ВСД2_ПроизводствоПартииСписания.Количество КАК КоличествоСписания,
	              |	ВСД_Партия.ДокОснование КАК ДокОснование,
	              |	ВСД_Партия.Ссылка КАК Партия,
	              |	ВСД_Партия.Количество КАК Количество,
	              |	ВСД_Партия.ДатаИзготовления1 КАК ДатаИзготовления1,
	              |	ВСД_Партия.Статус КАК СтатусПартии,
	              |	ПОДСТРОКА(ВСД_Партия.НомерЗаписи, 1, 10) КАК НомерЗаписи,
	              |	ВСД_Партия.Продукция_Элемент КАК Продукция,
				  |	ВСД_Партия.ВсдДата КАК ВсдДата
	              |ИЗ
	              |	Документ.ВСД2_Производство.ПартииСписания КАК ВСД2_ПроизводствоПартииСписания
	              |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВСД_Партия КАК ВСД_Партия
	              |		ПО ВСД2_ПроизводствоПартииСписания.Ссылка = ВСД_Партия.ДокОснование
	              |ГДЕ
	              |	ВСД_Партия.ДокОснование В (&ДокОснование)
	              |	И ВСД_Партия.Статус = ""103""
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ВСД2_ПроизводствоПартииСписания.Партия,
	              |	ВСД_Партия.ДокОснование,
	              |	ВСД_Партия.Ссылка,
	              |	ВСД2_ПроизводствоПартииСписания.Количество,
	              |	ВСД_Партия.Количество,
	              |	ВСД_Партия.ДатаИзготовления1,
	              |	ПОДСТРОКА(ВСД_Партия.НомерЗаписи, 1, 10),
	              |	ВСД_Партия.Статус,
	              |	ВСД_Партия.Продукция_Элемент";
	
	запрос.УстановитьПараметр("ДокОснование", СписокДокументов.ВыгрузитьЗначения());
	тзРезультатЗапроса = запрос.Выполнить().Выгрузить();
	
	тзРезультатЗапроса.Колонки.Добавить("ЛабИсследования");
	
	Для Каждого стр Из тзРезультатЗапроса Цикл
		лабы=стр.ПартияСписания.ЛабораторныеИсследования;
		Если лабы.Количество()=0 Тогда
			Продолжить;
		КонецЕсли;			
		
		новыйДок=документы.ВСД2_ЛабораторныеИсследования.СоздатьДокумент();
		новыйдок.дата=текущаядата();
		новыйдок.Партия = стр.Партия;
		новыйдок.Организация = Объект.Организация;
		новыйдок.Исследования.Загрузить( лабы.выгрузить() );
		Попытка
			новыйдок.записать(РежимЗаписиДокумента.Запись);	
		Исключение
			Сообщить(ОписаниеОшибки());
			Продолжить;
		КонецПопытки;
		стр.ЛабИсследования = НовыйДок.Ссылка;
	КонецЦикла;
	
	Объект.Партии.Загрузить( тзРезультатЗапроса );
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЛабИсследПоСырью(Команда)
	СоздатьЛабИсследПоСырьюНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсеНесозданные(Команда)
		
	Для Каждого стр Из Объект.Партии Цикл
		Если Стр.СтатусПартии = "102" ИЛИ Стр.СтатусПартии = "103" Тогда
			Если НЕ ЗначениеЗаполнено(стр.ЛабИсследования) Тогда
				Стр.Пометка = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СнятьВсеОтметки(Команда)
			
	Для Каждого стр Из Объект.Партии Цикл
		Если Стр.Пометка = Истина Тогда
			 Стр.Пометка = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбДокПроизводстваПриИзменении(Элемент)
	ЗаполнитьТабличныеЧасти();
КонецПроцедуры

&НаСервере
Функция ОтправитьВСДПакетно_НаСервере( )
	
	сзДокументыКОтправке = ПодготовитьСписокВСДКОтправке( );	
	
	ПараметрыФункции = кб99_ВСД.ЗагрузитьПараметры( Объект.Организация );
	ПараметрыФункции.Вставить("СписокДокументов", сзДокументыКОтправке);

	Если Объект.ОтправлятьВФоне Тогда 
		ИдентификаторЗадания = Неопределено;		
		НаименованиеЗадания = НСтр("ru = 'Ветис отправка Лабораторные исследования'");
		Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне( УникальныйИдентификатор, 
																"кб99_ВСД_Запросы.ВСД2_ЛабораторныеИсследования_ОтправитьПараллельно",
																ПараметрыФункции,
																НаименованиеЗадания);
		// результат обработки
		АдресХранилища       = Результат.АдресХранилища;		
		// для получения сообщений
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
	Иначе;
		Результат = кб99_ВСД_Запросы.ВСД2_ЛабораторныеИсследования_ОтправитьПараллельно( ПараметрыФункции );
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОтправитьПакетно(Команда)
	
	Состояние("Выполняется отправка ВСД",,"Ожидайте...",БиблиотекаКартинок.kb99_wrench);
	
	Результат = ОтправитьВСДПакетно_НаСервере();
	
	Если Результат.ЗаданиеВыполнено Тогда
		// Задание отработало, результат получен
		ПоказатьОповещениеПользователя("Выполнено");
	ИначеЕсли ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);
	КонецЕсли;			
	
	ЗаполнитьТабличныеЧасти();

КонецПроцедуры

&НаКлиенте
Процедура СоздатьПоПродукцииЭлементу(Команда)
	
	ОчиститьСообщения();
	
	СоздатьПоПродукцииЭлементуНаСервере();
	
	ПоказатьОповещениеПользователя("Выполнено");
	
КонецПроцедуры

&НаСервере
Процедура СоздатьПоПродукцииЭлементуНаСервере()
	
	тзПартии = Объект.Партии.Выгрузить(, "Продукция");
	тзПартии.Свернуть("Продукция");
	мсПродукцияКОформлению = тзПартии.ВыгрузитьКолонку("Продукция");

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВСД_Продукция_ЭлементИсследования.Ссылка КАК Продукция,
		|	ВСД_Продукция_ЭлементИсследования.НомерСтроки КАК НомерСтроки,
		|	ВСД_Продукция_ЭлементИсследования.НомерАктаОтбораПроб КАК НомерАктаОтбораПроб,
		|	ВСД_Продукция_ЭлементИсследования.ДатаОтбораПроб КАК ДатаОтбораПроб,
		|	ВСД_Продукция_ЭлементИсследования.НаименованиеЛаборатории КАК НаименованиеЛаборатории,
		|	ВСД_Продукция_ЭлементИсследования.НаименованиеПоказателя КАК НаименованиеПоказателя,
		|	ВСД_Продукция_ЭлементИсследования.ДатаРезультата КАК ДатаРезультата,
		|	ВСД_Продукция_ЭлементИсследования.МетодИсследования КАК МетодИсследования,
		|	ВСД_Продукция_ЭлементИсследования.НомерЭкспертизы КАК НомерЭкспертизы,
		|	ВСД_Продукция_ЭлементИсследования.РезультатИсследования КАК РезультатИсследования,
		|	ВСД_Продукция_ЭлементИсследования.Заключение КАК Заключение
		|ИЗ
		|	Справочник.ВСД_Продукция_Элемент.Исследования КАК ВСД_Продукция_ЭлементИсследования
		|ГДЕ
		|	ВСД_Продукция_ЭлементИсследования.НачалоДействия <= &ТекДата
		|	И ВСД_Продукция_ЭлементИсследования.КонецДействия >= &ТекДата
		|	И ВСД_Продукция_ЭлементИсследования.Ссылка В (&СписокПродукции)";
	
	Запрос.УстановитьПараметр("ТекДата", НачалоДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("СписокПродукции", мсПродукцияКОформлению);
	
	тзЛабИсследования = Запрос.Выполнить().Выгрузить();
	
	Для Каждого стрПартия Из Объект.Партии Цикл
		
		Если стрПартия.Пометка 
				И (СокрЛП(стрПартия.СтатусПартии) = "103" 
					ИЛИ СокрЛП(стрПартия.СтатусПартии) = "102") Тогда
			
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("Продукция", стрПартия.Продукция);
			СтрокиЛабИсследования = тзЛабИсследования.НайтиСтроки(СтруктураОтбора);			
			Если СтрокиЛабИсследования.Количество() Тогда
				
				Если ЗначениеЗаполнено(стрПартия.ЛабИсследования) И
									стрПартия.СтатусЗапроса = "Завершен" Тогда
					Продолжить;
				КонецЕсли;
									
				Если ЗначениеЗаполнено(стрПартия.ЛабИсследования) Тогда
									
					Док = стрПартия.ЛабИсследования.ПолучитьОбъект();
					Док.Исследования.Очистить();
					
				Иначе
					Док = Документы.ВСД2_ЛабораторныеИсследования.СоздатьДокумент();	
				КонецЕсли;
			
				Док.Дата = ТекущаяДатаСеанса();
				Док.Партия = стрПартия.Партия;
				Док.Организация = Объект.Организация;
				
				Для Каждого СтрЛаб Из СтрокиЛабИсследования Цикл
					
					нСтрДок = Док.Исследования.Добавить();
					ЗаполнитьЗначенияСвойств(нСтрДок, СтрЛаб);
					
				КонецЦикла;
			
				Док.Записать();
				
				стрПартия.ЛабИсследования = Док.Ссылка;
				
			Иначе
				кб99_ВСД.СообщитьИнфо(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("По продукции [%1] не указаны лабораторные исследования",
					стрПартия.Продукция), стрПартия.Продукция);	
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры