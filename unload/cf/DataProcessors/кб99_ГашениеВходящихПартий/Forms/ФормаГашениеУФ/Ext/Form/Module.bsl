// Форма Гашение для УП

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЗаполнитьВсдПоСправочнику();
КонецПроцедуры
//*******************  Открытие/Закрыте формы окончание

&НаСервере
Процедура ОтправкаЗапросовНаГашение( ПараметрыОрганизации = Неопределено ) Экспорт
	
	Отправлено = 0;
	Для Каждого СтрокаТЗ Из Объект.ВСДВходящие Цикл 
		Если НЕ(СтрокаТЗ.Отметка) Тогда
		    Продолжить;
		КонецЕсли;

		Если НЕ ЗначениеЗаполнено(ПараметрыОрганизации) Тогда 
			ПараметрыОрганизации = кб99_ВСД.ЗагрузитьПараметры( Объект.Организация );		
		КонецЕсли; 
		Если НЕ ЗначениеЗаполнено(СтрокаТЗ.ДокВСД.Получатель_Площадка) Тогда
			ДокВСД = СтрокаТЗ.ДокВСД.ПолучитьОбъект();
			ДокВСД.Получатель_Площадка = Объект.Отправитель_Площадка;
			ДокВСД.Записать();
		КонецЕсли;
		
		ПараметрыОрганизации.Отправитель_Площадка = СтрокаТЗ.ДокВСД.Получатель_Площадка;
		ПараметрыОрганизации.Вставить("флАктНесоответствия", флАктНесоответствия);
		ПараметрыОрганизации.Вставить("тПричинаАкта", тПричинаАкта);
		ПараметрыОрганизации.Вставить("тОписаниеНесоответствия", тОписаниеНесоответствия);
				
		СтрокаТЗ.СтатусЗапроса = кб99_ВСД_Запросы.ВСД2_Входящий_ОтправитьГашение( ПараметрыОрганизации, СтрокаТЗ.ДокВСД, СтрокаТЗ.ВСД_Партия );
		СтрокаТЗ.СтатусВСД = СтрокаТЗ.ДокВСД.СтатусВСД;
		
		Отправлено = Отправлено + 1;
		СтрокаТЗ.Отметка = 0;
	КонецЦикла;

КонецПроцедуры // Отправка()


&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	СписокКонстант = кб99_ВСД.ЗагрузитьПараметры( Объект.Организация );
	кб99_ВСД.ЗагрузитьПараметрыВОбработку( Объект, СписокКонстант ); 
	
	ЗаполнитьВсдПоСправочнику();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Объект.ВСДВходящие.Очистить();
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Отправитель_ПлощадкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ГУИДХСдляОтбора = кб99_ВСД.ПолучитьЗначениеРевизитаОбъекта_НаСервере(Объект.Отправитель_ХозСубъект, "GUID");
	ГУИДХСдляОтбора = ?(ЗначениеЗаполнено(ГУИДХСдляОтбора),ГУИДХСдляОтбора,"****");
	
	СтандартнаяОбработка = Ложь;
	ЗначениеОтбора = Новый Структура("GuidХозСубъекта", ГУИДХСдляОтбора);
	ПараметрыПодбора = Новый Структура("ЗакрыватьПриВыборе, РежимВыбора,Отбор", Истина, Истина,ЗначениеОтбора);	
	ОткрытьФорму("Справочник.ВСД_Площадка.ФормаВыбора", ПараметрыПодбора, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Отправитель_ПлощадкаПриИзменении(Элемент)
	
	УстановитьФильтрНаСервере();

КонецПроцедуры

// Вкладка Гашение

&НаКлиенте
Процедура ВСДВходящиеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

//&НаКлиенте
//Процедура кнОтметитьВсеДокументы(Команда)
//	Для Каждого стр Из объект.ВСДВходящие Цикл
//		стр.Отметка = Ложь;
//		Если ЗначениеЗаполнено(Стр.ВСД_Партия) Тогда
//			Продолжить;	
//		КонецЕсли;
//		Если НЕ(стр.Отметка)  Тогда 
//			стр.Отметка = Истина;
//		КонецЕсли;
//	КонецЦикла;
//КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметкиДокументов(Команда)
	
	Для Каждого стр Из Объект.ВСДВходящие Цикл
		Если (стр.Отметка) Тогда 
			стр.Отметка = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьВсдВходящие()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	кб99_ВСД2.Ссылка
	               |ИЗ
	               |	Справочник.кб99_ВСД2 КАК кб99_ВСД2
	               |ГДЕ
	               |	кб99_ВСД2.Получатель_ХозСубъект = &Получатель_ХозСубъект
	               |	И кб99_ВСД2.Получатель_Площадка = &Получатель_Площадка
	               |	И кб99_ВСД2.ПометкаУдаления = ЛОЖЬ
	               |	И НЕ кб99_ВСД2.СтатусВСД ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("Получатель_ХозСубъект", Объект.Отправитель_ХозСубъект);
	Запрос.УстановитьПараметр("Получатель_Площадка", Объект.Отправитель_Площадка);
	тзВыборка = Запрос.Выполнить().Выгрузить();
	
	Для Каждого строкаВыборки ИЗ тзВыборка Цикл
		
		МенеджерЗаписи = РегистрыСведений.кб99_Запросы.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Период = ТекущаяДата();
		МенеджерЗаписи.Объект = строкаВыборки.Ссылка;
		МенеджерЗаписи.ApplicationID = "НЕТ";
		МенеджерЗаписи.СтатусЗапроса = "НЕТ";
		МенеджерЗаписи.Пользователь = ПараметрыСеанса.ТекущийПользователь;		
		МенеджерЗаписи.Записать();

	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура кнПолучитьВСДНаСервере()
	
	УдалитьВсдВходящие();
	
	ПараметрыОрганизации = кб99_ВСД.ЗагрузитьПараметры( Объект.Организация );
	Попытка
		ИскатьДокументПрихода = ПараметрыОрганизации["ПарамВсдВходящиеИскатьДокументПрихода"];
	Исключение
		ИскатьДокументПрихода = Ложь;
		кб99_ВСД.СообщитьИнфо("Не заполнен параметр [ПарамВсдВходящиеИскатьДокументПрихода]. Пожалуйста, проверьте и сохраните Параметры интеграции.");
	КонецПопытки;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("ПолучательПлощадка", Объект.Отправитель_Площадка);	
	ПараметрыЗапроса.Вставить("vetDocumentType", "INCOMING");	
	ПараметрыЗапроса.Вставить("vetDocumentStatus", "CONFIRMED");	
	ПараметрыЗапроса.Вставить("Смещение", 0);	
	ПараметрыЗапроса.Вставить("ПоступилиС", ВыбначалоПериода);	
	ПараметрыЗапроса.Вставить("ПоступилиПо", ВыбОкончаниеПериода);	
	ПараметрыЗапроса.Вставить("ОтправительХозСубъект", ВыбрХозСубъект);	
	ПараметрыЗапроса.Вставить("ОтправительПлощадка", ВыбрПлощадка);	
	ПараметрыЗапроса.Вставить("ПолучательПлощадка", Объект.Отправитель_Площадка);	
	
	Запросов = 20;
	ВсдМассив = Новый Массив;
	
	Результат = кб99_ВСД_Запросы.ПолучитьСписокВСД( ПараметрыОрганизации, ПараметрыЗапроса, ВсдМассив );
	// Если результат REJECTED, думаю ни к чему отправлять некорректный запрос в цикле. Проанализировать ошибку и повторно 
	// отправить запрос вручную.
	//Пока Результат = "REJECTED" и (Запросов > 0) Цикл
	//	Запросов = Запросов - 1;
	//	кб99_ВСД.СообщитьИнфо("Попыток запроса ВСД осталось "+Запросов);
	//	Результат = кб99_ВСД_Запросы.ПолучитьСписокВСД( ПараметрыОрганизации, ПараметрыЗапроса, ВсдМассив );
	//КонецЦикла;
	
	Объект.ВСДВходящие.Очистить();
	Для Каждого строкаВСД Из ВсдМассив Цикл
		СтрокаВСДВходящие = Объект.ВСДВходящие.Добавить();
		ЗаполнитьЗначенияСвойств( СтрокаВСДВходящие, строкаВСД );
		ГашениеЗаполнитьСтрокуТаблицы( СтрокаВСДВходящие, ИскатьДокументПрихода ); //+
		Если ИскатьДокументПрихода Тогда 
			Гашение_ЗаполнитьСтрокуПоПоступлениеТоваров( СтрокаВСДВходящие );
		КонецЕсли;
	КонецЦикла;
	
	УстановитьФильтрНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура кнПолучитьВСД(Команда)
	
	ПоказатьОповещениеПользователя("Выполняем запрос Входящих ВСД по "+Объект.Отправитель_Площадка,,"Ожидайте...",БиблиотекаКартинок.kb99_wrench);
	кнПолучитьВСДНаСервере();
	ПоказатьОповещениеПользователя("Выполнено");
	
КонецПроцедуры

&НаКлиенте
Процедура кнПогасить(Команда)
	
	ПоказатьОповещениеПользователя("Выполняем запрос Гашения Входящих ВСД",,"Ожидайте...",БиблиотекаКартинок.kb99_wrench);
	
	ОтправкаЗапросовНаГашение();
	
	ПоказатьОповещениеПользователя("Выполнено");	
	
КонецПроцедуры

// ***** Соответствия
&НаСервере
Функция ПолучитьНоменклатуруПоПродукцияЭлемент(ПродукцияЭлемент, Только1элемент = Ложь)
	
	Возврат кб99_ВСД.ПолучитьНоменклатуруПоПродукцияЭлемент( ПродукцияЭлемент, Только1элемент);
	
КонецФункции

&НаСервере
Функция НайтиНоменклатуруПоРеквизиту(ВыбРеквизит,ЗначениеРеквизита)
	
	Рез = "";
	Запрос = Новый Запрос;
	ТекстЗапроса = "
	|Выбрать Номенклатура.Ссылка из Справочник.Номенклатура как Номенклатура 
	|где Номенклатура.@ИмяРекв = &ЗначРеквизита";
	текстЗапроса = СтрЗаменить(ТекстЗапроса,"@ИмяРекв",ВыбРеквизит);
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ЗначРеквизита", ЗначениеРеквизита);
	Попытка
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Рез = Выборка.Ссылка;	
		КонецЕсли;
	Исключение
		кб99_ВСД.СообщитьИнфо("Что-то пошло не так");
		кб99_ВСД.СообщитьИнфо(ОписаниеОшибки());
	КонецПопытки;
	Возврат Рез;
	
КонецФункции

&НаСервере
Процедура ОчиститьСоответствие(ВыбНоменклатура,ВСДЭлемент)
	
	кб99_ВСД.ОчиститьСоответствие_ВСД_Продукция_Элемент( ВыбНоменклатура, ВСДЭлемент );
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТЗСоответствий(сзПродукцияЭлементы)
	
	//Вынести в модуль объекта
	// Заполнение Продукция_элементом  + сразу поиск установленных соответствий
	// сзПродукцияЭлементы - Список Значений
	Объект.Соответствия.Очистить();
	тз = Объект.Соответствия.Выгрузить(); //Взяли структуру
	Для Каждого стрТз Из сзПродукцияЭлементы Цикл
		стрСоотв = тз.Добавить();
		стрСоотв.Продукция_Элемент = стрТз.Значение;//Продукция_Элемент;
		стрСоотв.Производитель = стрСоотв.Продукция_Элемент.Площадка;
		стрСоотв.Артикул = стрСоотв.Продукция_Элемент.Артикул;
		стрСоотв.GTIN = стрСоотв.Продукция_Элемент.GTIN;
		стрСоотв.Номенклатура = ПолучитьНоменклатуруПоПродукцияЭлемент(стрСоотв.Продукция_Элемент, 1);
		Если ЗначениеЗаполнено(стрСоотв.Номенклатура) Тогда
			стрСоотв.Записано = true;	
		КонецЕсли;
	КонецЦикла;
	//Свернем, т.к. могут повторяться
	тз.Свернуть("Продукция_Элемент,Номенклатура,Артикул,GTIN,Производитель,Записано","");
	Объект.Соответствия.Загрузить(тз);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСПоискомПоРеквизиту(ВыбРеквизит,ИмяРекв = "Артикул")
	
	тз = Объект.Соответствия.Выгрузить();
	Для Каждого строкаТз Из тз Цикл
		Если (строкаТз.Записано) или (ЗначениеЗаполнено(строкаТз.Номенклатура)) Тогда
			Продолжить;
		КонецЕсли;
		Если ИмяРекв = "Артикул" Тогда
		    ВыбЗначение = СокрЛП(строкаТз.Артикул);
		Иначе
		    ВыбЗначение = СокрЛП(строкаТз.GTIN);
		КонецЕсли;
		Если НЕ(ЗначениеЗаполнено(ВыбЗначение)) Тогда
			Продолжить;	
		КонецЕсли;		
		строкаТз.Номенклатура = НайтиНоменклатуруПоРеквизиту(ВыбРеквизит,ВыбЗначение);
	КонецЦикла;
	Объект.Соответствия.Загрузить(Тз);
	
КонецПроцедуры

#Область Кнопки

&НаСервере
Процедура кнЗаполнитьТзСоответствийСервер()
	
	Фобъект = РеквизитФормыВЗначение("Объект");
	сзЭлементы = Новый СписокЗначений;
	сзЭлементы.ЗагрузитьЗначения(Фобъект.ВсдВходящие.ВыгрузитьКолонку("Продукция_Элемент"));
	ЗаполнитьТЗСоответствий(сзЭлементы);
	
КонецПроцедуры

&НаКлиенте
Процедура кнЗаполнитьТзСоответствий(Команда)
	кнЗаполнитьТзСоответствийСервер();
КонецПроцедуры

&НаСервере
Процедура кнЗаполнитьИзСправочникаНаСервере()
	
	// Заполним из Справочника ВСД_Продукция_Элемент
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ВСД_Продукция_Элемент.Ссылка
	               |ИЗ
	               |	Справочник.ВСД_Продукция_Элемент КАК ВСД_Продукция_Элемент
	               |ГДЕ
	               |	ВСД_Продукция_Элемент.ПометкаУдаления = ЛОЖЬ
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ВСД_Продукция_Элемент.Наименование";
	Запрос.Текст = ТекстЗапроса;
	ТзВрем = Запрос.Выполнить().Выгрузить();
	сзЭлементы = Новый СписокЗначений;
	сзЭлементы.ЗагрузитьЗначения(ТзВрем.ВыгрузитьКолонку("Ссылка"));
	ЗаполнитьТЗСоответствий(сзЭлементы);
	
КонецПроцедуры

&НаКлиенте
Процедура кнЗаполнитьИзСправочника(Команда)
	кнЗаполнитьИзСправочникаНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура кнНайтиПоАртикулу(Команда)
	ЗаполнитьСПоискомПоРеквизиту(ИмяРеквизитаАртикул,"Артикул");
КонецПроцедуры

&НаКлиенте
Процедура кнНайтиПоШК(Команда)
	ЗаполнитьСПоискомПоРеквизиту(ИмяРеквизитаШК,"ШК");
КонецПроцедуры

&НаСервере
Процедура кнЗаписатьСоответствияНаСервере()
	
	тз = Объект.Соответствия.Выгрузить();
	Для Каждого строкаТз Из тз Цикл
		Если (строкаТз.Записано) или НЕ(ЗначениеЗаполнено(строкаТз.Номенклатура)) Тогда
			Продолжить;
		КонецЕсли;
		кб99_ВСД.Установить_Соответствие_ВСД_Продукция_Элемент(СтрокаТЗ.Номенклатура,СтрокаТЗ.Продукция_Элемент);
		строкаТЗ.Записано = True;
	КонецЦикла;
	Объект.Соответствия.Загрузить(Тз);
	
КонецПроцедуры

&НаКлиенте
Процедура кнЗаписатьСоответствия(Команда)
	кнЗаписатьСоответствияНаСервере();
КонецПроцедуры

&НаСервере
Процедура кнУбратьЗаполненныеНаСервере()
	
	тз = Объект.Соответствия.Выгрузить();
	Найдено = тз.Найти(True,"Записано"); 
	Пока НЕ(Найдено = Неопределено) Цикл
		тз.Удалить(Найдено);
		Найдено = тз.Найти(True,"Записано");
	КонецЦикла;
	Объект.Соответствия.Загрузить(Тз);
	
КонецПроцедуры

&НаКлиенте
Процедура кнУбратьЗаполненные(Команда)
	кнУбратьЗаполненныеНаСервере();
КонецПроцедуры
 
&НаКлиенте
Процедура кнФильтр(Команда)
	
	Элементы.ГруппаОтбора.Видимость = Не Элементы.ГруппаОтбора.Видимость;
	
КонецПроцедуры

&НаКлиенте
Процедура кнПогаситьСАктомНесоответствия(Команда)
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПослеЗаполненияАктаНесоответствия", ЭтотОбъект);
	ОткрытьФорму("Обработка.кб99_ГашениеВходящихПартий.Форма.ОформлениеАктаНесоответстсвия",,
					ЭтаФорма,,,, ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

// События Соответствия
&НаКлиенте
Процедура СоответствияНоменклатураНачалоВыбораОтвет(Ответ,Парам) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОчиститьСоответствие(Парам.Номенклатура,Парам.Продукция_Элемент);
		Парам.Номенклатура = "";
		Парам.Записано = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоответствияНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Элемент.Родитель.ТекущиеДанные.Записано Тогда
		ТекстВопроса = "Установлена связь с 
		|"+Элемент.Родитель.ТекущиеДанные.Продукция_Элемент+"
		|Отвязываем ?";
		Парам = Новый Структура("Номенклатура,Продукция_Элемент,Записано",Элемент.Родитель.ТекущиеДанные.Номенклатура,Элемент.Родитель.ТекущиеДанные.Продукция_Элемент,Элемент.Родитель.ТекущиеДанные.Записано);
    	Оповещение = Новый ОписаниеОповещения("СоответствияНоменклатураНачалоВыбораОтвет",ЭтаФорма,Парам);	
    	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,  0, КодВозвратаДиалога.Да, ""   );
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция Получить_ВСД_Продукция_ЭлементНаСервере(ВыбНоменклатура)
	
	Возврат  кб99_ВСД.Продукция_Элемент_ПолучитьПоНоменклатуре(ВыбНоменклатура);
	
КонецФункции

&НаКлиенте
Процедура СоответствияНоменклатураПриИзмененииОтвет(Ответ,Парам) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
   		ОчиститьСоответствие(Парам.Элемент.Родитель.ТекущиеДанные.Номенклатура,Парам.ВремПЭлемент);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоответствияНоменклатураПриИзменении(Элемент)
	
	ВремПЭлемент = Получить_ВСД_Продукция_ЭлементНаСервере(Элемент.Родитель.ТекущиеДанные.Номенклатура);
	Если НЕ ЗначениеЗаполнено(ВремПЭлемент) Тогда
		Элемент.Родитель.ТекущиеДанные.Записано = False;
	ИначеЕсли НЕ(ВремПЭлемент = Элемент.Родитель.ТекущиеДанные.Продукция_Элемент) Тогда
		ТекстВопроса = "Уже установлена связь с 
		|"+ВремПЭлемент+"
		|Отвязываем ?";
		Парам = Новый Структура("Элемент,ВремПЭлемент",Элемент,ВремПЭлемент);
    	Оповещение = Новый ОписаниеОповещения("СоответствияНоменклатураПриИзмененииОтвет",ЭтаФорма,Парам);	
    	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,  0, КодВозвратаДиалога.Да, ""   );
		Элемент.Родитель.ТекущиеДанные.Записано = False;
	Иначе
		Элемент.Родитель.ТекущиеДанные.Записано = True;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СоответствияНоменклатураОчисткаОтвет(Ответ, Парам) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОчиститьСоответствие(Парам.Элемент.Родитель.ТекущиеДанные.Номенклатура,Парам.Элемент.Родитель.ТекущиеДанные.Продукция_Элемент);
		Парам.Элемент.Родитель.ТекущиеДанные.Номенклатура = "";
		Парам.Элемент.Родитель.ТекущиеДанные.Записано = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоответствияНоменклатураОчистка(Элемент, СтандартнаяОбработка)
	
	Если Элемент.Родитель.ТекущиеДанные.Записано Тогда
		СтандартнаяОбработка = Ложь;
		ТекстВопроса = "Установлена связь с 
		|"+Элемент.Родитель.ТекущиеДанные.Продукция_Элемент+"
		|Отвязываем ?";
		Парам = Новый Структура("Элемент,СтандартнаяОбработка",Элемент);
    	Оповещение = Новый ОписаниеОповещения("СоответствияНоменклатураОчисткаОтвет",ЭтаФорма,Парам);	
    	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,  0, КодВозвратаДиалога.Да, ""   );
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоответствияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ВыборПериодаВСД(Период, ДополнительныеПараметры) Экспорт
	
	Диалог = ДополнительныеПараметры.Диалог;
	ВыбранныйПериод = Диалог.Период;
	ВыбНачалоПериода = ВыбранныйПериод.ДатаНачала;
	ВыбокончаниеПериода = ВыбранныйПериод.ДатаОкончания;
	
	УстановитьФильтрНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура кнВыбратьПериодВСД(Команда)
	
	ВыбранныйПериод = Новый СтандартныйПериод;
	Если Команда.Имя = "кнВыбратьПериодВСД" Тогда
		// Устанавливаем начальные данные
		ВыбранныйПериод.ДатаНачала = ВыбНачалоПериода;
		ВыбранныйПериод.ДатаОкончания = ВыбокончаниеПериода;
		КудаРезультат = "ВыборПериодаВСД";
	КонецЕсли;
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период = ВыбранныйПериод;
	// Не модальный вызов диалога выбора периода
		
	Диалог.Показать(Новый ОписаниеОповещения(КудаРезультат, ЭтаФорма, Новый Структура("Диалог", Диалог)));
		
КонецПроцедуры

&НаСервере
Процедура кнОформитьВходящийВСДНаСервере()
	
	Фобъект = РеквизитФормыВЗначение("Объект");
	ФОбъект.СоздатьВСД_ВходящиеПоТЗВходящих();
	ЗначениеВРеквизитФормы(Фобъект,"Объект");

КонецПроцедуры

&НаКлиенте
Процедура кнОформитьВходящийВСДОтвет(Ответ,Парам) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
   		кнОформитьВходящийВСДНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура кнОформитьВходящийВСД(Команда)
   	Оповещение = Новый ОписаниеОповещения("кнОформитьВходящийВСДОтвет",ЭтаФорма);	
   	ПоказатьВопрос(Оповещение, "Создать документы в 1С ?", РежимДиалогаВопрос.ДаНет,  0, КодВозвратаДиалога.Да, ""   );
КонецПроцедуры

&НаСервере
Процедура УстановитьФильтрНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Отправитель_Площадка) Тогда
		ЗаполнитьВсдПоСправочнику();
	Иначе
		ЗаполнитьВсдПоВсемПлощадкам();
	КонецЕсли;
	
	НомерСтроки = Объект.ВСДВходящие.Количество();
	Пока НомерСтроки > 0 Цикл	
		НомерСтроки=НомерСтроки-1;
		ТекСтрока = Объект.ВСДВходящие[НомерСтроки];

		Если ЗначениеЗаполнено(ВыбНачалоПериода) и ТекСтрока.ВСДДата < ВыбНачалоПериода Тогда 
			кб99_ВСД.СообщитьИнфо("Отфильтрована партия "+ТекСтрока.ВСДДата+" ["+ТекСтрока.НаименованиеПродукции+"] = "+ТекСтрока.Количество );
			Объект.ВСДВходящие.Удалить(Номерстроки);
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(ВыбОкончаниеПериода) и ТекСтрока.ВСДДата > ВыбОкончаниеПериода Тогда 
			кб99_ВСД.СообщитьИнфо("Отфильтрована партия "+ТекСтрока.ВСДДата+" ["+ТекСтрока.НаименованиеПродукции+"] = "+ТекСтрока.Количество );
			Объект.ВСДВходящие.Удалить(Номерстроки);
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(ВыбрХозСубъект) и ТекСтрока.Отправитель_Хозсубъект <> ВыбрХозСубъект Тогда 
			кб99_ВСД.СообщитьИнфо("Отфильтрована партия "+ТекСтрока.Отправитель_Хозсубъект+" ["+ТекСтрока.НаименованиеПродукции+"] = "+ТекСтрока.Количество );
			Объект.ВСДВходящие.Удалить(Номерстроки);
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(ВыбрПлощадка) и ТекСтрока.Отправитель_Площадка <> ВыбрПлощадка Тогда 
			кб99_ВСД.СообщитьИнфо("Отфильтрована партия "+ТекСтрока.Отправитель_Площадка+" ["+ТекСтрока.НаименованиеПродукции+"] = "+ТекСтрока.Количество );
			Объект.ВСДВходящие.Удалить(Номерстроки);
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(НомерТТН) и ТекСтрока.ТтнНомер <> НомерТТН Тогда 
			кб99_ВСД.СообщитьИнфо("Отфильтрована партия "+ТекСтрока.ВСДДата+" ["+ТекСтрока.НаименованиеПродукции+"] = "+ТекСтрока.Количество );
			Объект.ВСДВходящие.Удалить(НомерСтроки);			
			Продолжить;
		КонецЕсли;
		Если ГашениеСВХ и ЗначениеЗаполнено(текСтрока.Отправитель_Площадка) Тогда
			кб99_ВСД.СообщитьИнфо("Отфильтрована партия "+ТекСтрока.ВСДДата+" ["+ТекСтрока.НаименованиеПродукции+"] = "+ТекСтрока.Количество );
			Объект.ВСДВходящие.Удалить(НомерСтроки);
			Продолжить;
		КонецЕсли;		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбНачалоПериодаПриИзменении(Элемент)
	УстановитьФильтрНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВыбОкончаниеПериодаПриИзменении(Элемент)
	УстановитьФильтрНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВыбрХозСубъектПриИзменении(Элемент)
	УстановитьФильтрНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВыбрПлощадкаПриИзменении(Элемент)
	УстановитьФильтрНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Отправитель_ХозСубъект") Тогда 
		Если НЕ ЗначениеЗаполнено( Параметры.Отправитель_ХозСубъект ) Тогда 
			кб99_ВСД.СообщитьИнфо("Не заполнен параметр [Отправитель_ХозСубъект] для загрузки параметров");
			Возврат;			
		КонецЕсли;
		Объект.Организация = кб99_ВСД.ПолучитьОрганизациюПоХС( Параметры.Отправитель_ХозСубъект );
		
		Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда 
			кб99_ВСД.СообщитьИнфо("Не найдена организация для загрузки параметров по "+Параметры.Отправитель_ХозСубъект);
			Возврат;
		КонецЕсли;
		
		ПараметрыОрганизации = кб99_ВСД.ЗагрузитьПараметры( Объект.Организация );		
		кб99_ВСД.ЗагрузитьПараметрыВОбработку( Объект, ПараметрыОрганизации ); 					
	КонецЕсли;
    	
	Если Параметры.Свойство("Отправитель_Площадка") Тогда 
		
		Если НЕ ЗначениеЗаполнено( Параметры.Отправитель_Площадка ) Тогда 
			кб99_ВСД.СообщитьИнфо("Не заполнен параметр [Отправитель_Площадка]");
			Возврат;			
		КонецЕсли;
		
		Объект.Отправитель_Площадка = Параметры.Отправитель_Площадка;		
		ПараметрыОрганизации.Отправитель_Площадка = Объект.Отправитель_Площадка;
		
	КонецЕсли;
	
	Если Параметры.Свойство("ТаблицаВсдДляГашения") Тогда 
		
		Объект.ВСДВходящие.Очистить();
		Для Каждого строкаВСД Из Параметры.ТаблицаВсдДляГашения Цикл

			СтрокаВСДВходящие = Объект.ВСДВходящие.Добавить();
			СтрокаВСДВходящие.Отметка = Истина;
			ЗаполнитьЗначенияСвойств( СтрокаВСДВходящие, строкаВСД );
		КонецЦикла;
		ОтправкаЗапросовНаГашение( ПараметрыОрганизации );

	КонецЕсли;
	
	Если Объект.Организация = Справочники.Организации.ПустаяСсылка() Тогда 
			
		// заполнения по умолчению
		Объект.Организация = кб99_ВСД_Общий.ПолучитьОрганизациюПоУмолчанию();	
		ПараметрыОрганизации = кб99_ВСД.ЗагрузитьПараметры( Объект.Организация );
		кб99_ВСД.ЗагрузитьПараметрыВОбработку( Объект, ПараметрыОрганизации ); 
				
	КонецЕсли;
	
	Элементы.ГруппаОтбора.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ВСДВходящиеКоличествоВозвратПриИзменении(Элемент)
	
	ВыбЭлемент = ЭтаФорма.Элементы.ВСДВходящие.ТекущиеДанные;

	Если НЕ ГашениеСВХ Тогда
		ВыбЭлемент.КоличествоПринять = ВыбЭлемент.Количество - ВыбЭлемент.КоличествоВозврат;
		ВыбЭлемент.Отметка = Истина;
		ВСДВходящиеКоличествоПриИзмененииНаСервере( ВыбЭлемент.ДокВСД, ВыбЭлемент.КоличествоПринять, ВыбЭлемент.КоличествоВозврат )
	Иначе
		Сообщение = "При гашении товаров с СВХ можно принимать только в полном объеме!";
		кб99_ВСД.СообщитьПользователю(Сообщение,,ВыбЭлемент.КоличествоВозврат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура кнПереместитьПартии(Команда)
	
	тзПартий = Новый Массив;
	
	Для Каждого стрВходящие Из объект.ВСДВходящие Цикл
		Если стрВходящие.Отметка И ЗначениеЗаполнено( стрВходящие.ВСД_Партия ) Тогда 
			СтрПартий = Новый Структура("Партия, Продукция_Элемент, Количество, ЕдиницаИзмерения, Цель");
			ЗаполнитьЗначенияСвойств( СтрПартий, стрВходящие );
			СтрПартий.Партия = стрВходящие.ВСД_Партия;
			СтрПартий.Количество = стрВходящие.КоличествоПринять;
			тзПартий.Добавить( стрПартий );
		КонецЕсли;
	КонецЦикла;

	Если тзПартий.Количество()>0 Тогда 
		
		ПараметрыДокумента = кб99_ВСД.ЗагрузитьПараметры( Объект.Организация );
		ЭлементыОтбора = Новый Структура("ПараметрыДокумента, Партии", ПараметрыДокумента, тзПартий);
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЭлементыОтбора);

		ОткрытьФорму("Документ.ВСД2_транзакция.ФормаОбъекта", ПараметрыФормы );
				
	Иначе
		Сообщить("Нечего перемещать, ВСД_Партии не выбраны");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВсдПоСправочнику()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	кб99_ВСД2.Ссылка КАК ДокВСД,
	               |	кб99_ЗапросыСрезПоследних.СтатусЗапроса,
	               |	кб99_ЗапросыСрезПоследних.Пользователь,
	               |	кб99_ЗапросыСрезПоследних.ApplicationID,
	               |	кб99_ВСД2.Отправитель_ХозСубъект,
	               |	кб99_ВСД2.Отправитель_Площадка,
	               |	кб99_ВСД2.Получатель_ХозСубъект,
	               |	кб99_ВСД2.Получатель_Площадка,
	               |	кб99_ВСД2.Количество,
	               |	кб99_ВСД2.ТтнСерия,
	               |	кб99_ВСД2.ТтнНомер,
	               |	кб99_ВСД2.ТтнДата,
	               |	кб99_ВСД2.номерАвто,
	               |	кб99_ВСД2.UUID,
	               |	кб99_ВСД2.ФормаВСД,
	               |	кб99_ВСД2.ЕдиницаИзмерения,
	               |	кб99_ВСД2.ТермическоеСостояние,
	               |	кб99_ВСД2.ОсобыеОтметки,
	               |	кб99_ВСД2.cargoInspected,
	               |	кб99_ВСД2.ВсдСерия,
	               |	кб99_ВСД2.ВсдНомер,
	               |	кб99_ВСД2.ВсдДата,
	               |	кб99_ВСД2.Продукция,
	               |	кб99_ВСД2.ВидПродукции,
	               |	кб99_ВСД2.НаименованиеПродукции,
	               |	кб99_ВСД2.Продукция_Элемент,
	               |	кб99_ВСД2.ДокументОснование,
	               |	кб99_ВСД2.ВидВСД,
	               |	кб99_ВСД2.Скоропортящийся,
	               |	кб99_ВСД2.Некачественный,
	               |	кб99_ВСД2.ТипТС,
	               |	кб99_ВСД2.Организация,
	               |	кб99_ВСД2.ДатаИзготовления,
	               |	кб99_ВСД2.ДатаСрокГодности,
	               |	кб99_ВСД2.номерПолуприцепа,
	               |	кб99_ВСД2.номерКонтейнера,
	               |	кб99_ВСД2.ТипВСД,
	               |	кб99_ВСД2.GTIN,
	               |	кб99_ВСД2.Артикул,
	               |	кб99_ВСД2.ФасовкаФормаУпаковки,
	               |	кб99_ВСД2.ФасовкаНаименование,
	               |	кб99_ВСД2.ФасовкаКоличество,
	               |	кб99_ВСД2.ФасовкаЕдиницаИзм,
	               |	кб99_ВСД2.ФасовкаОбъем,
	               |	кб99_ВСД2.ВладелецХС,
	               |	кб99_ВСД2.ТтнТип,
	               |	кб99_ВСД2.КоличествоПринять,
	               |	кб99_ВСД2.КоличествоВозврат,
	               |	кб99_ВСД2.ТермическиеУсловияПеревозки,
	               |	кб99_ВСД2.ДатаИзготовления1,
	               |	кб99_ВСД2.ДатаИзготовления2,
	               |	кб99_ВСД2.ДатаСрокГодности1,
	               |	кб99_ВСД2.ДатаСрокГодности2,
	               |	кб99_ВСД2.Страна
	               |ИЗ
	               |	Справочник.кб99_ВСД2 КАК кб99_ВСД2
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.кб99_Запросы.СрезПоследних КАК кб99_ЗапросыСрезПоследних
	               |		ПО кб99_ВСД2.Ссылка = кб99_ЗапросыСрезПоследних.Объект
	               |ГДЕ
	               |	кб99_ВСД2.ПометкаУдаления = ЛОЖЬ
	               |	И НЕ кб99_ЗапросыСрезПоследних.СтатусЗапроса = ""НЕТ""
	               |	И кб99_ВСД2.СтатусВСД = &ВыбСтатус
	               |	И кб99_ВСД2.Получатель_Площадка = &Получатель_Площадка";
	
	Запрос.УстановитьПараметр("ВыбСтатус", Перечисления.кб99_СтатусВСД.CONFIRMED );
	Запрос.УстановитьПараметр("Получатель_Площадка", Объект.Отправитель_Площадка );
	
	Выборка = Запрос.Выполнить().Выгрузить();
	
	
	Объект.ВСДВходящие.Очистить();
	ПараметрыОрганизации = кб99_ВСД.ЗагрузитьПараметры( Объект.Организация );
	Для Каждого строкаВыборка Из Выборка Цикл
		СтрокаВСДВходящие = Объект.ВСДВходящие.Добавить();
		ЗаполнитьЗначенияСвойств( СтрокаВСДВходящие, строкаВыборка );
		Попытка
			ИскатьДокументПрихода = ПараметрыОрганизации["ПарамВсдВходящиеИскатьДокументПрихода"];
		Исключение
			ИскатьДокументПрихода = Ложь;
			кб99_ВСД.СообщитьИнфо("Не заполнен параметр [ПарамВсдВходящиеИскатьДокументПрихода]. Пожалуйста, проверьте и сохраните Параметры интеграции.");
		КонецПопытки;
		
		Попытка
			ИскатьДокументПрихода = ПараметрыОрганизации["ПарамВсдВходящиеИскатьДокументПрихода"];
		Исключение
			ИскатьДокументПрихода = Ложь;
			кб99_ВСД.СообщитьИнфо("Не заполнен параметр [ПарамВсдВходящиеИскатьДокументПрихода]. Пожалуйста, проверьте и сохраните Параметры интеграции.");
		КонецПопытки;
		
		ГашениеЗаполнитьСтрокуТаблицы( СтрокаВСДВходящие, ИскатьДокументПрихода );
				
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция Гашение_НайтиСрокГодностиПоСерии( СтрокаТовары )
	
	Ответ = 0;
	СерииЗаполнены = Ложь;
	
	тзСерии = СтрокаТовары.ДокументПрихода.Серии.Выгрузить();
	Для каждого строкаСерия Из тзСерии Цикл
		Если строкаСерия.Номенклатура =  СтрокаТовары.Номенклатура Тогда 
			
			СерииЗаполнены = Истина;
			ДатаСрокГодности1 = кб99_ВСД_Запросы.СтрокаВДатаВремя( СтрокаТовары.ДатаСрокГодности1 );
			СтрокаТовары.ДатаСрокГодности1С = строкаСерия.Серия.ГоденДо;
			
				Если ЗначениеЗаполнено( СтрокаТовары.ДатаСрокГодности2 ) Тогда 
					
					ДатаСрокГодности2 = кб99_ВСД_Запросы.СтрокаВДатаВремя( СтрокаТовары.ДатаСрокГодности2 );
					
					Если строкаСерия.Серия.ГоденДо >= ДатаСрокГодности1 И 
						 строкаСерия.Серия.ГоденДо <= ДатаСрокГодности2 Тогда 
						 Ответ = Ответ + строкаСерия.Количество;
					 КонецЕсли;
				 Иначе 
					 Если строкаСерия.Серия.ГоденДо = ДатаСрокГодности1 Тогда 
						Ответ = Ответ + строкаСерия.Количество; 
					КонецЕсли;				 
				КонецЕсли;
		КонецЕсли;
	КонецЦикла;
		
	Если НЕ СерииЗаполнены	Тогда 
		Ответ = СтрокаТовары.Количество;
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

&НаСервере
Процедура Гашение_ЗаполнитьСтрокуПоПоступлениеТоваров( СтрокаВСДВходящие ) Экспорт
	
	ПереопределенныйМодуль = кб99_ВСД_Общий.ФункцияПереопределена("Гашение_ЗаполнитьСтрокуПоПоступлениеТоваров");
	Если ПереопределенныйМодуль <> Неопределено Тогда		
		ПереопределенныйМодуль.Гашение_ЗаполнитьСтрокуПоПоступлениеТоваров( СтрокаВСДВходящие );
		Возврат;
	КонецЕсли;	
	
	Попытка
		Если ЗначениеЗаполнено(СтрокаВСДВходящие.ДокументПрихода) Тогда 
			тзТовары = СтрокаВСДВходящие.ДокументПрихода.Товары.Выгрузить();
		    тзТовары.Свернуть("Номенклатура", "Количество");
			СтрокаТовары =  тзТовары.Найти( СтрокаВСДВходящие.Номенклатура, "Номенклатура");
			Если ЗначениеЗаполнено( СтрокаТовары ) Тогда 
				
				КоличествоСерия = Гашение_НайтиСрокГодностиПоСерии( СтрокаВСДВходящие );
				Если СтрокаТовары.Номенклатура.ВесЗнаменатель>0 Тогда 
					Вес = СтрокаТовары.Номенклатура.ВесЧислитель / СтрокаТовары.Номенклатура.ВесЗнаменатель;
				Иначе
					Вес = 1;
				КонецЕсли;
				Если КоличествоСерия > 0 Тогда 				
					СтрокаВСДВходящие.КоличествоПринято1С = КоличествоСерия*вес;
				Иначе					
					СтрокаВСДВходящие.КоличествоПринято1С = СтрокаТовары.Количество*вес;					
				КонецЕсли;
								
			Иначе
				СтрокаВСДВходящие.Ошибки = "Не найдена номенклатура ["+СтрокаВСДВходящие.Номенклатура+"] в документе ["+СтрокаВСДВходящие.ДокументПрихода+"]";
				кб99_ВСД.СообщитьИнфо( СтрокаВСДВходящие.Ошибки , СтрокаВСДВходящие.ДокументПрихода );
			КонецЕсли;
		КонецЕсли;
	Исключение
		кб99_ВСД.СообщитьИнфо("Не удалось найти документ ПриходнаяНакладная, переопределите функцию [Гашение_ЗаполнитьСтрокуПоПоступлениеТоваров] для Вашей конфигурации "+символы.ПС+ОписаниеОшибки());		
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ГашениеЗаполнитьСтрокуТаблицы( СтрокаВСДВходящие, ИскатьДокументПрихода )
	
	СтрокаВСДВходящие.Номенклатура = кб99_ВСД.ПолучитьНоменклатуруПоПродукцияЭлемент( СтрокаВСДВходящие.Продукция_Элемент, Истина );  
	Если ИскатьДокументПрихода Тогда
		СтрокаВСДВходящие.ДокументПрихода = кб99_ВСД_Общий.Гашение_НайтиДокументПоступлениеТоваров( СтрокаВСДВходящие.ДокВСД.ТтнДата, СтрокаВСДВходящие.ДокВСД.ТтнНомер, СтрокаВСДВходящие.Отправитель_Хозсубъект.ИНН );
		СтрокаВСДВходящие.КодТНВЭД=СтрокаВСДВходящие.Продукция_Элемент.ВидПродукции.КодТНВЭД+"/"+СтрокаВСДВходящие.Продукция_Элемент.Продукция.КодТНВЭД;
		Если ЗначениеЗаполнено( СтрокаВСДВходящие.ДокументПрихода ) Тогда 
			Гашение_ЗаполнитьСтрокуПоПоступлениеТоваров( СтрокаВСДВходящие );
		КонецЕсли;
	КонецЕсли;
	кб99_ВСД_Общий.ГашениеПроверитьСтрокуТаблицы(СтрокаВСДВходящие, ИскатьДокументПрихода);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВСДВходящиеКоличествоПриИзмененииНаСервере( ДокСсылка, КоличествоПринять, КоличествоВозврат )
	
	ДокОбъект = ДокСсылка.ПолучитьОбъект();
	ДокОбъект.КоличествоПринять = КоличествоПринять;
	ДокОбъект.КоличествоВозврат = КоличествоВозврат;
	ДокОбъект.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ВСДВходящиеКоличествоПринятьПриИзменении(Элемент)
	
	ВыбСтрока = ЭтаФорма.Элементы.ВСДВходящие.ТекущиеДанные;
	ВСДВходящиеКоличествоПриИзмененииНаСервере( ВыбСтрока.ДокВСД, ВыбСтрока.КоличествоПринять, ВыбСтрока.КоличествоВозврат );
	
КонецПроцедуры

&НаКлиенте
Процедура кнСписатьПартии(Команда)
		
	тзПартий = Новый Массив;
	
	Для Каждого стрВходящие Из объект.ВСДВходящие Цикл
		Если стрВходящие.Отметка И ЗначениеЗаполнено( стрВходящие.ВСД_Партия ) Тогда 
			СтрПартий = Новый Структура("Партия, Продукция_Элемент, Количество, ЕдиницаИзмерения, Цель");
			ЗаполнитьЗначенияСвойств( СтрПартий, стрВходящие );
			СтрПартий.Партия = стрВходящие.ВСД_Партия;
			СтрПартий.Количество = 0;
			тзПартий.Добавить( стрПартий );
		КонецЕсли;
	КонецЦикла;

	Если тзПартий.Количество()>0 Тогда 
		
		ПараметрыДокумента = кб99_ВСД.ЗагрузитьПараметры( Объект.Организация );
		ПараметрыДокумента.Отправитель_Площадка = Объект.Отправитель_Площадка;
		ЭлементыОтбора = Новый Структура("ПараметрыДокумента, Партии", ПараметрыДокумента, тзПартий);
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЭлементыОтбора);

		ОткрытьФорму("Документ.ВСД2_Инвентаризация.ФормаОбъекта", ПараметрыФормы );
				
	Иначе
		Сообщить("Нечего перемещать, ВСД_Партии не выбраны");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура кнПолучитьВсдПоВсемПлощадкамНаСервере()
	
	УдалитьВсдВходящие();
	Объект.Отправитель_Площадка = Неопределено;
	Объект.ВСДВходящие.Очистить();
	ПараметрыОрганизации = кб99_ВСД.ЗагрузитьПараметры( Объект.Организация );
		
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВСД_Площадка.Ссылка КАК Площадка
	|ИЗ
	|	Справочник.ВСД_Площадка КАК ВСД_Площадка
	|ГДЕ
	|	ВСД_Площадка.GuidХозСубъекта = &GuidХозСубъекта
	|	И НЕ ВСД_Площадка.ПометкаУдаления
	|	И НЕ ВСД_Площадка.ИгнорироватьПриГрупповомГашении";
	Запрос.УстановитьПараметр("GuidХозСубъекта", ПараметрыОрганизации.Отправитель_ХозСубъект.GUID);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ПараметрыЗапроса = Новый Структура;
		ПараметрыЗапроса.Вставить("vetDocumentType", "INCOMING");	
		ПараметрыЗапроса.Вставить("vetDocumentStatus", "CONFIRMED");	
		ПараметрыЗапроса.Вставить("Смещение", 0);	
		ПараметрыЗапроса.Вставить("ПоступилиС", ВыбначалоПериода);	
		ПараметрыЗапроса.Вставить("ПоступилиПо", ВыбОкончаниеПериода);	
		ПараметрыЗапроса.Вставить("ОтправительХозСубъект", ВыбрХозСубъект);	
		ПараметрыЗапроса.Вставить("ОтправительПлощадка", ВыбрПлощадка);	
		ПараметрыЗапроса.Вставить("ПолучательПлощадка", Выборка.Площадка);	
		
		Результат = кб99_ВСД_Запросы.ПолучитьСписокВСД( ПараметрыОрганизации, ПараметрыЗапроса );
	КонецЦикла;	
	УстановитьФильтрНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура кнПолучитьВсдПоВсемПлощадкам(Команда)
	
	ПоказатьОповещениеПользователя("Выполняем запрос Входящих ВСД по всем площадкам ",,"Ожидайте...",БиблиотекаКартинок.kb99_wrench);
	кнПолучитьВсдПоВсемПлощадкамНаСервере();
	ПоказатьОповещениеПользователя("Выполнено");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВсдПоВсемПлощадкам() 
	
	ПараметрыОрганизации = кб99_ВСД.ЗагрузитьПараметры( Объект.Организация );
	Попытка
		ИскатьДокументПрихода = ПараметрыОрганизации["ПарамВсдВходящиеИскатьДокументПрихода"];
	Исключение
		ИскатьДокументПрихода = Ложь;
		кб99_ВСД.СообщитьИнфо("Не заполнен параметр [ПарамВсдВходящиеИскатьДокументПрихода]. Пожалуйста, проверьте и сохраните Параметры интеграции.");
	КонецПопытки;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВСД_Площадка.Ссылка КАК Площадка
	|ИЗ
	|	Справочник.ВСД_Площадка КАК ВСД_Площадка
	|ГДЕ
	|	ВСД_Площадка.GuidХозСубъекта = &GuidХозСубъекта";
	Запрос.УстановитьПараметр( "GuidХозСубъекта", ПараметрыОрганизации.Отправитель_ХозСубъект.GUID );
	ТзПлощадки = Запрос.Выполнить().Выгрузить();

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	кб99_ВСД2.Ссылка КАК ДокВСД,
	               |	кб99_ЗапросыСрезПоследних.СтатусЗапроса,
	               |	кб99_ЗапросыСрезПоследних.Пользователь,
	               |	кб99_ЗапросыСрезПоследних.ApplicationID,
	               |	кб99_ВСД2.Отправитель_ХозСубъект,
	               |	кб99_ВСД2.Отправитель_Площадка,
	               |	кб99_ВСД2.Получатель_ХозСубъект,
	               |	кб99_ВСД2.Получатель_Площадка,
	               |	кб99_ВСД2.Количество,
	               |	кб99_ВСД2.ТтнСерия,
	               |	кб99_ВСД2.ТтнНомер,
	               |	кб99_ВСД2.ТтнДата,
	               |	кб99_ВСД2.номерАвто,
	               |	кб99_ВСД2.UUID,
	               |	кб99_ВСД2.ФормаВСД,
	               |	кб99_ВСД2.ЕдиницаИзмерения,
	               |	кб99_ВСД2.ТермическоеСостояние,
	               |	кб99_ВСД2.ОсобыеОтметки,
	               |	кб99_ВСД2.cargoInspected,
	               |	кб99_ВСД2.ВсдСерия,
	               |	кб99_ВСД2.ВсдНомер,
	               |	кб99_ВСД2.ВсдДата,
	               |	кб99_ВСД2.Продукция,
	               |	кб99_ВСД2.ВидПродукции,
	               |	кб99_ВСД2.НаименованиеПродукции,
	               |	кб99_ВСД2.Продукция_Элемент,
	               |	кб99_ВСД2.ДокументОснование,
	               |	кб99_ВСД2.ВидВСД,
	               |	кб99_ВСД2.Скоропортящийся,
	               |	кб99_ВСД2.Некачественный,
	               |	кб99_ВСД2.ТипТС,
	               |	кб99_ВСД2.Организация,
	               |	кб99_ВСД2.ДатаИзготовления,
	               |	кб99_ВСД2.ДатаСрокГодности,
	               |	кб99_ВСД2.номерПолуприцепа,
	               |	кб99_ВСД2.номерКонтейнера,
	               |	кб99_ВСД2.ТипВСД,
	               |	кб99_ВСД2.GTIN,
	               |	кб99_ВСД2.Артикул,
	               |	кб99_ВСД2.ФасовкаФормаУпаковки,
	               |	кб99_ВСД2.ФасовкаНаименование,
	               |	кб99_ВСД2.ФасовкаКоличество,
	               |	кб99_ВСД2.ФасовкаЕдиницаИзм,
	               |	кб99_ВСД2.ФасовкаОбъем,
	               |	кб99_ВСД2.ВладелецХС,
	               |	кб99_ВСД2.ТтнТип,
	               |	кб99_ВСД2.КоличествоПринять,
	               |	кб99_ВСД2.КоличествоВозврат,
	               |	кб99_ВСД2.ТермическиеУсловияПеревозки,
	               |	кб99_ВСД2.ДатаИзготовления1,
	               |	кб99_ВСД2.ДатаИзготовления2,
	               |	кб99_ВСД2.ДатаСрокГодности1,
	               |	кб99_ВСД2.ДатаСрокГодности2,
	               |	кб99_ВСД2.Страна
	               |ИЗ
	               |	Справочник.кб99_ВСД2 КАК кб99_ВСД2
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.кб99_Запросы.СрезПоследних КАК кб99_ЗапросыСрезПоследних
	               |		ПО кб99_ВСД2.Ссылка = кб99_ЗапросыСрезПоследних.Объект
	               |ГДЕ
	               |	кб99_ВСД2.ПометкаУдаления = ЛОЖЬ
	               |	И НЕ кб99_ЗапросыСрезПоследних.СтатусЗапроса = ""НЕТ""
	               |	И кб99_ВСД2.СтатусВСД = &ВыбСтатус
	               |	И кб99_ВСД2.Получатель_Площадка В (&ТзПлощадки)";
	
	Запрос.УстановитьПараметр( "ВыбСтатус", Перечисления.кб99_СтатусВСД.CONFIRMED );
	Запрос.УстановитьПараметр( "ТзПлощадки", ТзПлощадки );
	Выборка = Запрос.Выполнить().Выгрузить();
		
	Объект.ВСДВходящие.Очистить();
	Для Каждого строкаВыборка Из Выборка Цикл
		СтрокаВСДВходящие = Объект.ВСДВходящие.Добавить();
		ЗаполнитьЗначенияСвойств( СтрокаВСДВходящие, строкаВыборка );
		ГашениеЗаполнитьСтрокуТаблицы(СтрокаВСДВходящие, ИскатьДокументПрихода);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВСДВходящиеДатаСрокГодности1ПриИзменении(Элемент)
	
	Ответ = Вопрос("Изменить срок годности?",РежимДиалогаВопрос.ДаНет,60,,,);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ВыбСтрока = ЭтаФорма.Элементы.ВСДВходящие.ТекущиеДанные;
		Ссылка = ВыбСтрока.ДокВСД;
		ДатаСрока = ЭтаФорма.Элементы.ВСДВходящие.ТекущиеДанные.ДатаСрокГодности1;
		ВСДВходящиеДатаСрокГодности1ПриИзмененииНаСервере(Ссылка,ДатаСрока);
	Иначе
		ВыбСтрока = ЭтаФорма.Элементы.ВСДВходящие.ТекущиеДанные;
		Ссылка = ВыбСтрока.ДокВСД;
		ЭтаФорма.Элементы.ВСДВходящие.ТекущиеДанные.ДатаСрокГодности1 =	Ссылка.ДатаСрокГодности1;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВСДВходящиеДатаСрокГодности1ПриИзмененииНаСервере(Ссылка,ДатаСрока)
	
	ДокОбъект = Ссылка.ПолучитьОбъект();
	ДокОбъект.ДатаСрокГодности1=ДатаСрока;
	ДокОбъект.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура флАктНесоответствияПриИзменении(Элемент)
	
	Если флАктНесоответствия Тогда
		Элемент.ЦветТекстаЗаголовка=WebЦвета.Красный;
		ЭтаФорма.Элементы.тПричинаАкта.ЦветТекстаЗаголовка=WebЦвета.Красный;
		ЭтаФорма.Элементы.тОписаниеНесоответствия.ЦветТекстаЗаголовка=WebЦвета.Красный;	
	Иначе
		Элемент.ЦветТекстаЗаголовка=WebЦвета.Черный;
		ЭтаФорма.Элементы.тПричинаАкта.ЦветТекстаЗаголовка=WebЦвета.Черный;
		ЭтаФорма.Элементы.тОписаниеНесоответствия.ЦветТекстаЗаголовка=WebЦвета.Черный;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьДокОбновитьТранспортНаСервере(НомерАвто)
	
	Для Каждого СтрВходящие ИЗ Объект.ВСДВходящие Цикл
		Если ЗначениеЗаполнено(СтрВходящие.НомерАвто) ИЛИ ЗначениеЗаполнено(СтрВходящие.ДокОбновитьТранспорт)
			ИЛИ Не СтрВходящие.Отметка Тогда
			Продолжить;
		Иначе
			ДокОбновитьТранспорт = Документы.ВСД2_ОбновитьТранспорт.СоздатьДокумент();
			ПараметрыДокумента = Новый Структура("Организация, Получатель_Площадка , Дата", 
												 Объект.Организация, Объект.Отправитель_Площадка, ТекущаяДата());
			ЭлементыОтбора = Новый Структура("ПараметрыДокумента, ДокВсд, НомерАвто", ПараметрыДокумента, СтрВходящие.ДокВСД, НомерАвто);
			ДокОбновитьТранспорт.Заполнить(ЭлементыОтбора);
			ДокОбновитьТранспорт.Записать();
			СтрВходящие.ДокОбновитьТранспорт = ДокОбновитьТранспорт.Ссылка;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаНомераАвто(Строка, Параметры) Экспорт
	
	Если НЕ Строка = Неопределено Тогда
		СоздатьДокОбновитьТранспортНаСервере(Строка);
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УказатьНомерАвто(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПослеВводаНомераАвто", ЭтаФорма);
	ОткрытьФорму("ОбщаяФорма.кб99_ФормаВводаНомераАвто",, ЭтаФорма,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры

&НаСервере
Процедура ОтправкаДокументовВсдОбновитьТранспорт()
	
	Отправлено = 0;
	Для Каждого СтрокаТЗ Из Объект.ВСДВходящие Цикл 
		Если НЕ(СтрокаТЗ.Отметка) ИЛИ НЕ ЗначениеЗаполнено(СтрокаТЗ.ДокОбновитьТранспорт) Тогда
		    Продолжить;
		КонецЕсли;

		ПараметрыОрганизации = кб99_ВСД.ЗагрузитьПараметры( Объект.Организация );		
  		ПараметрыОрганизации.Отправитель_Площадка = СтрокаТЗ.ДокВСД.Получатель_Площадка;
		
		СтрокаТЗ.СтатусЗапроса = кб99_ВСД_Запросы.ВСД2_ОбновитьТранспорт_Отправить( ПараметрыОрганизации, СтрокаТЗ.ДокОбновитьТранспорт );
		СтрокаТЗ.НомерАвто = СтрокаТЗ.ДокВСД.НомерАвто;
		
		Отправлено = Отправлено + 1;
		СтрокаТЗ.Отметка = 0;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура кнОтправитьДокВсдИзменитьНомерАвто(Команда)
	
	ПоказатьОповещениеПользователя("Выполняем запрос на изменение номера автомобиля",,"Ожидайте...",БиблиотекаКартинок.kb99_wrench);
	
	ОтправкаДокументовВсдОбновитьТранспорт();
	
	ПоказатьОповещениеПользователя("Выполнено");
	
КонецПроцедуры

&НаСервере
Процедура кнПогаситьПараллельноНаСервере()
	
	ПараметрыОрганизации = кб99_ВСД.ЗагрузитьПараметры( Объект.Организация );		
	СписокВСДкГашению = Новый Массив;
	
	Для Каждого СтрокаТЗ Из Объект.ВСДВходящие Цикл 
		Если НЕ СтрокаТЗ.Отметка ИЛИ ЗначениеЗаполнено(СтрокаТЗ.ВСД_Партия) Тогда
			Продолжить;
		КонецЕсли;
		СписокВСДкГашению.Добавить(СтрокаТЗ.ДокВСД);
	КонецЦикла;
	
	ПараметрыОрганизации.Вставить("СписокДокументов", СписокВСДкГашению);
	ПараметрыОрганизации.Вставить("флАктНесоответствия", флАктНесоответствия);
	ПараметрыОрганизации.Вставить("тПричинаАкта", тПричинаАкта);
	ПараметрыОрганизации.Вставить("тОписаниеНесоответствия", тОписаниеНесоответствия);
	
	кб99_ВСД_Запросы.ВСД2_Входящий_ОтправитьГашение_Параллельно( ПараметрыОрганизации, Неопределено );
				
	Для Каждого СтрокаТз Из Объект.ВСДВходящие Цикл
		СтрокаТЗ.СтатусВСД = СтрокаТЗ.ДокВСД.СтатусВСД;
		СтрокаТЗ.Отметка = 0;
	КонецЦикла;	
		
КонецПроцедуры

&НаКлиенте
Процедура кнПогаситьПараллельно(Команда)
	
	ПоказатьОповещениеПользователя("Выполняем запрос Гашения Входящих ВСД",,"Ожидайте...",БиблиотекаКартинок.kb99_wrench);
	
	кнПогаситьПараллельноНаСервере();
	
	ПоказатьОповещениеПользователя("Выполнено");	

КонецПроцедуры

&НаКлиенте
Процедура НомерТТНПриИзменении(Элемент)
	УстановитьФильтрНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура кнРаспределитьПродукцию(Команда)
	
	массивВСД = Новый Массив;
	Для Каждого стр из Объект.ВСДВходящие Цикл
		Если стр.Отметка И ЗначениеЗаполнено(стр.ВСД_Партия) Тогда
        	СтрПартий = Новый Структура("ВСД_Партия, Количество, ЕдиницаИзмерения");
			ЗаполнитьЗначенияСвойств(СтрПартий,стр);
			массивВСД.Добавить(СтрПартий);
		КонецЕсли;
	КонецЦикла;
	
	Если массивВСД.Количество()>0 Тогда 
		ПараметрыФормы = Новый Структура("ВСД", массивВСД);
		ПослеРаспределенияПродукцииСВХ = Новый ОписаниеОповещения("ПослеРаспределенияПродукцииСВХ",ЭтаФорма);
		ОткрытьФорму("Обработка.кб99_ГашениеВходящихПартий.Форма.ГашениеСВХ", ПараметрыФормы,ЭтаФорма,,,,ПослеРаспределенияПродукцииСВХ);
	Иначе
		Сообщить("Распределить возможно только погашенные партии!");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеРаспределенияПродукцииСВХ(РезультатЗакрытия, допПараметры = неопределено) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		ПараметрыДокумента = кб99_ВСД.ЗагрузитьПараметры( Объект.Организация );
		ПараметрыДокумента.Отправитель_Площадка = Объект.Отправитель_Площадка;
		ЭлементыОтбора = Новый Структура("ПараметрыДокумента, РаспределеннаяПродукция", ПараметрыДокумента, РезультатЗакрытия);
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЭлементыОтбора);
		ОткрытьФорму("Документ.ВСД2_Инвентаризация.ФормаОбъекта", ПараметрыФормы );						
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГашениеСВХПриИзменении(Элемент)
	УстановитьФильтрНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВыбрПлощадкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ГУИДХСдляОтбора = кб99_ВСД.ПолучитьЗначениеРевизитаОбъекта_НаСервере(ВыбрХозСубъект,"GUID");
	ГУИДХСдляОтбора = ?(ЗначениеЗаполнено(ГУИДХСдляОтбора),ГУИДХСдляОтбора,"****");
	
	СтандартнаяОбработка = Ложь;
	ЗначениеОтбора = Новый Структура("GuidХозСубъекта", ГУИДХСдляОтбора);
	ПараметрыПодбора = Новый Структура("ЗакрыватьПриВыборе, РежимВыбора,Отбор", Истина, Истина,ЗначениеОтбора);	
	ОткрытьФорму("Справочник.ВСД_Площадка.ФормаВыбора", ПараметрыПодбора, Элемент);

КонецПроцедуры

&НаКлиенте
Процедура кнОтметитьПринятыеНаСклад(Команда)
	
	Для Каждого Стр Из Объект.ВСДВходящие Цикл
		Стр.Отметка = Ложь;
		Если ЗначениеЗаполнено(Стр.ВСД_Партия) ИЛИ ЗначениеЗаполнено(Стр.Ошибки) Тогда
			Продолжить;	
		КонецЕсли;
		Если НЕ(Стр.Отметка) Тогда 
			Стр.Отметка = Истина;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура кнОтметитьТолькоВыбранные(Команда)
	Для Каждого ВыделеннаяСтрока  Из Элементы.ВСДВходящие.ВыделенныеСтроки Цикл
		стр = Элементы.ВСДВходящие.ДанныеСтроки(ВыделеннаяСтрока);
		стр.Отметка = Ложь;
		Если ЗначениеЗаполнено(Стр.ВСД_Партия) Тогда
			Продолжить;	
		КонецЕсли;
		Если НЕ(стр.Отметка) Тогда 
			стр.Отметка = Истина;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#Область ОформлениеПоступления

&НаСервере
Функция ПроверитьВозможностьОформленияПоступления()
	
	Попытка
		Возврат Метаданные.ОбщиеМодули.Найти("Пользователи") <> Неопределено
				И Метаданные.ОбщиеМодули.Найти("ЗначениеНастроекПовтИсп") <> Неопределено
				И Метаданные.ОбщиеМодули.Найти("ДенежныеСредстваСервер") <> Неопределено
				И Метаданные.ОбщиеМодули.Найти("ЗакупкиСервер") <> Неопределено
				И Метаданные.ОбщиеМодули.Найти("ВзаиморасчетыСервер") <> Неопределено
				И Метаданные.ОбщиеМодули.Найти("УчетНДСУП") <> Неопределено
				И Метаданные.ОбщиеМодули.Найти("РаботаСКурсамиВалютУТ") <> Неопределено
				И Метаданные.ОбщиеМодули.Найти("ОбработкаТабличнойЧастиСервер") <> Неопределено
				И Метаданные.ОбщиеМодули.Найти("ОбработкаТабличнойЧастиКлиентСервер") <> Неопределено
				И Метаданные.ОбщиеМодули.Найти("НоменклатураСервер") <> Неопределено;
	Исключение
		Возврат Ложь;
	КонецПопытки;
			
КонецФункции

&НаСервере
Функция СоздатьНовуюСерию(ВидНоменклатуры, ДатаПроизводства, ГоденДо)

	НоваяСерия = Справочники.СерииНоменклатуры.СоздатьЭлемент();
	НоваяСерия.ВидНоменклатуры = ВидНоменклатуры;
	
	НомерЧислом = Справочники.СерииНоменклатуры.ВычислитьМаксимальныйНомерСерии(ВидНоменклатуры) + 1;
	Номер = Формат(НомерЧислом, "ЧЦ=8; ЧВН=; ЧГ=");

	НоваяСерия.Наименование = Номер;
	НоваяСерия.ДатаПроизводства = кб99_ВСД_Запросы.СтрокаВДату(ДатаПроизводства);
	НоваяСерия.ГоденДо = кб99_ВСД_Запросы.СтрокаВДату(ГоденДо);
	НоваяСерия.Номер = Номер;
	НоваяСерия.Записать();
	
	Возврат НоваяСерия.Ссылка

КонецФункции

&НаСервере
Функция НайтиСерию(ВидНоменклатуры, ДатаПроизводства, ГоденДо)

	Серия = Справочники.СерииНоменклатуры.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СерииНоменклатуры.Ссылка КАК Серия
	|ИЗ
	|	Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|ГДЕ
	|	СерииНоменклатуры.ВидНоменклатуры = &ВидНоменклатуры
	|	И СерииНоменклатуры.ГоденДо = &ГоденДо
	|	И СерииНоменклатуры.ДатаПроизводства = &ДатаПроизводства
	|	И НЕ СерииНоменклатуры.ПометкаУдаления";
	Запрос.УстановитьПараметр("ВидНоменклатуры", ВидНоменклатуры);
	Запрос.УстановитьПараметр("ДатаПроизводства", кб99_ВСД_Запросы.СтрокаВДату(ДатаПроизводства));
	Запрос.УстановитьПараметр("ГоденДо", кб99_ВСД_Запросы.СтрокаВДату(ГоденДо));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Серия = Выборка.Серия;
	КонецЕсли;
	
	Возврат Серия;

КонецФункции

&НаСервере
Процедура ИнициализироватьДокументПриобретение(Док)
	
	мПользователи			 = кб99_ВСД_Общий.ОбщийМодуль("Пользователи");
	мЗначениеНастроекПовтИсп = кб99_ВСД_Общий.ОбщийМодуль("ЗначениеНастроекПовтИсп");
	мДенежныеСредстваСервер	 = кб99_ВСД_Общий.ОбщийМодуль("ДенежныеСредстваСервер");
	мЗакупкиСервер			 = кб99_ВСД_Общий.ОбщийМодуль("ЗакупкиСервер"); 
	мВзаиморасчетыСервер	 = кб99_ВСД_Общий.ОбщийМодуль("ВзаиморасчетыСервер"); 
	мУчетНДСУП				 = кб99_ВСД_Общий.ОбщийМодуль("УчетНДСУП");
	мРаботаСКурсамиВалютУТ	 = кб99_ВСД_Общий.ОбщийМодуль("РаботаСКурсамиВалютУТ");
	
	Док.Менеджер                  = мПользователи.ТекущийПользователь();
	Док.Валюта                    = мЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(Док.Валюта);
	Док.ВалютаВзаиморасчетов      = мЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(Док.ВалютаВзаиморасчетов);
	
	Док.Подразделение             = мЗначениеНастроекПовтИсп.ПодразделениеПользователя(Док.Менеджер, Док.Подразделение);
	
	СтруктураПараметров = мДенежныеСредстваСервер.ПараметрыЗаполненияБанковскогоСчетаОрганизацииПоУмолчанию();
	СтруктураПараметров.Организация    		= Док.Организация;
	СтруктураПараметров.БанковскийСчет		= Док.БанковскийСчетОрганизации;
	Док.БанковскийСчетОрганизации = мЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(СтруктураПараметров);
	
	Док.БанковскийСчетКонтрагента = мЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетКонтрагентаПоУмолчанию(Док.Контрагент, , Док.БанковскийСчетКонтрагента);
	Док.Склад                     = Объект.Отправитель_Площадка.Склад;
	Док.ПорядокРасчетов           = мВзаиморасчетыСервер.ПорядокРасчетовПоДокументу(Док);
	
	СтруктураОтветственного = мЗакупкиСервер.ПолучитьОтветственногоПоСкладу(Док.Склад, Док.Менеджер);
	Если СтруктураОтветственного <> Неопределено Тогда
		Док.Принял = СтруктураОтветственного.Ответственный;
		Док.ПринялДолжность = СтруктураОтветственного.ОтветственныйДолжность;
	КонецЕсли;
	
	Док.ВариантПриемкиТоваров = мЗакупкиСервер.ПолучитьВариантПриемкиТоваров(, Док.Договор);
	
	мРаботаСКурсамиВалютУТ.ЗаполнитьКурсКратностьПоУмолчанию(Док.Курс, Док.Кратность, Док.Валюта, Док.ВалютаВзаиморасчетов);
	
	ПараметрыЗаполнения = Документы.ПриобретениеТоваровУслуг.ПараметрыЗаполненияНалогообложенияНДСЗакупки(Док);
	мУчетНДСУП.ЗаполнитьНалогообложениеНДСЗакупки(Док.НалогообложениеНДС, ПараметрыЗаполнения);
	
	ПараметрыЗаполнения = Документы.ПриобретениеТоваровУслуг.ПараметрыЗаполненияВидаДеятельностиНДС(Док);
	мУчетНДСУП.ЗаполнитьВидДеятельностиНДС(Док.ЗакупкаПодДеятельность, ПараметрыЗаполнения);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтрокуПриобретения(ТекущаяСтрока, Док, СтрОснование)
	
	мОбработкаТабличнойЧастиСервер		 = кб99_ВСД_Общий.ОбщийМодуль("ОбработкаТабличнойЧастиСервер");
	мОбработкаТабличнойЧастиКлиентСервер = кб99_ВСД_Общий.ОбщийМодуль("ОбработкаТабличнойЧастиКлиентСервер");
	мНоменклатураСервер					 = кб99_ВСД_Общий.ОбщийМодуль("НоменклатураСервер");
	
	ПараметрыУказанияСерий = Новый ФиксированнаяСтруктура(мНоменклатураСервер.ПараметрыУказанияСерий(Док, Документы.ПриобретениеТоваровУслуг));
	
	СтруктураПересчетаСуммы = мОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Док);
	
	СтруктураДействий = Новый Структура;
	
	ВидЦены = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВидыЦенПоставщиков.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ВидыЦенПоставщиков КАК ВидыЦенПоставщиков
	|ГДЕ
	|	ВидыЦенПоставщиков.Владелец = &Владелец
	|	И НЕ ВидыЦенПоставщиков.ПометкаУдаления";
	Запрос.УстановитьПараметр("Владелец", Док.Партнер);
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		ВидЦены = Результат.Ссылка;	
	КонецЕсли;
	
	ПараметрыОрганизации = кб99_ВСД.ЗагрузитьПараметры(Объект.Организация);
	Если ПараметрыОрганизации.ПарамИспользоватьСерииПродукции Тогда
		СерияНоменклатуры = НайтиСерию(ТекущаяСтрока.Номенклатура.ВидНоменклатуры, СтрОснование.ДатаИзготовления1, СтрОснование.ДатаСрокГодности1);
		Если Не ЗначениеЗаполнено(СерияНоменклатуры) Тогда
			СерияНоменклатуры = СоздатьНовуюСерию(ТекущаяСтрока.Номенклатура.ВидНоменклатуры, СтрОснование.ДатаИзготовления1, СтрОснование.ДатаСрокГодности1);
		КонецЕсли;
		ТекущаяСтрока.Серия = СерияНоменклатуры;
	КонецЕсли;	
	//Если ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСПоставщиками") И ЗначениеЗаполнено(Док.Соглашение) Тогда
	//	СтруктураДействий.Вставить("ЗаполнитьУсловияЗакупок", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруЗаполненияЦеныЗакупкиВСтрокеТЧ(Док));
	//Иначе
	//	СтруктураЗаполненияЦены = Новый Структура("Соглашение,Партнер,Дата,Валюта,Соглашение,НалогообложениеНДС,ВернутьМногооборотнуюТару,Партнер,Дата,Валюта,ВидЦеныПоставщика");
	//	ЗаполнитьЗначенияСвойств(СтруктураЗаполненияЦены,Объект);
	//	СтруктураЗаполненияЦены.ВидЦеныПоставщика = ВидЦены;
	//	СтруктураДействий.Вставить("ЗаполнитьЦенуЗакупки", СтруктураЗаполненияЦены);	
	//КонецЕсли;
	ТекущаяСтрока.Цена = 100;

	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДС", Новый Структура("НалогообложениеНДС, Дата", Док.НалогообложениеНДС, Док.Дата));
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСВозвратнойТары", Док.ВернутьМногооборотнуюТару);
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));
	СтруктураДействий.Вставить("ОчиститьСуммуВзаиморасчетов");
	СтруктураДействий.Вставить("ПроверитьСериюРассчитатьСтатус", Новый Структура("Склад, ПараметрыУказанияСерий", ТекущаяСтрока.Склад, ПараметрыУказанияСерий));
	СтруктураДействий.Вставить("ПересчитатьСуммуСверхЗаказа", Новый Структура("РеализацияПоступлениеПоЗаказу, ТребуетсяЗалогЗаТару", 
								Док.ПоступлениеПоЗаказам, Док.ТребуетсяЗалогЗаТару));
	
	мОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, Неопределено);
		
КонецПроцедуры

&НаСервере
Функция РассчитатьКоличествоПрихода(СтрокаТЧДок)
	
	Коэфф = 1;
	
	Если СтрокаТЧДок.Номенклатура.ВесИспользовать Тогда
		Попытка
			Если СтрокаТЧДок.Номенклатура.ВесЗнаменатель>0 Тогда 
				Коэфф=СтрокаТЧДок.Номенклатура.ВесЧислитель/СтрокаТЧДок.Номенклатура.ВесЗнаменатель;
				Если СтрокаТЧДок.Номенклатура.ВесЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду("163") Тогда
					Коэфф = Коэфф / 1000;
				КонецЕсли;
			Иначе
				Коэфф=1;
			КонецЕсли;
		Исключение
			Коэфф = 1;
		КонецПопытки;
	КонецЕсли;
	
	Рез = СтрокаТЧДок.Количество/Коэфф;
	
	Возврат Рез;
	
КонецФункции

&НаСервере
Функция ОформитьЗакупкуУПоставщикаНаСервере()
	
	СформированныеДокументы = Новый Массив;
	
	мЗакупкиСервер = кб99_ВСД_Общий.ОбщийМодуль("ДенежныеСредстваСервер");
	
	тзВСД = Объект.ВСДВходящие.Выгрузить().Скопировать(Новый Структура("Отметка", Истина));
	
	Если Не тзВСД.Количество() Тогда
		кб99_ВСД.СообщитьИнфо("Не выбраны строки к оформлению");
		Возврат СформированныеДокументы;
	КонецЕсли;
	
	тзПоТТН = тзВСД.Скопировать();
	тзПоТТН.Колонки.Добавить("Контрагент");
	Для Каждого Стр Из тзПоТТН Цикл
		Если ЗначениеЗаполнено(Стр.Отправитель_Хозсубъект)
				И ЗначениеЗаполнено(Стр.Отправитель_Хозсубъект.ИНН) Тогда
			Стр.Контрагент = Справочники.Контрагенты.НайтиПоРеквизиту("ИНН", Стр.Отправитель_Хозсубъект.ИНН);		
		КонецЕсли;
	КонецЦикла;
	тзПоТТН.Свернуть("ТтнНомер, ТтнДата, Контрагент");
	
	Для Каждого ТТН Из тзПоТТН Цикл
		Если Не ЗначениеЗаполнено(ТТН.Контрагент) Тогда
			кб99_ВСД.СообщитьИнфо(СтрШаблон("Не удалось найти контрагента по накладной №%1 от %2", ТТН.ТтнНомер, ТТН.ТтнДата));
			Продолжить;
		КонецЕсли;
		
		ПараметрыОтбора = Новый	Структура("ТтнНомер, ТтнДата");
		ЗаполнитьЗначенияСвойств(ПараметрыОтбора, ТТН);
		СтрокиТовары = тзВСД.НайтиСтроки(ПараметрыОтбора);
		
		ДокументПрихода = Документы.ПриобретениеТоваровУслуг.СоздатьДокумент();
		ДокументПрихода.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика;
		ДокументПрихода.Дата = ТекущаяДатаСеанса();
		ДокументПрихода.ДатаВходящегоДокумента = ТТН.ТтнДата;
		ДокументПрихода.НомерВходящегоДокумента = ТТН.ТтнНомер;
		ДокументПрихода.Организация = Объект.Организация;
		ДокументПрихода.СпособДоставки = Перечисления.СпособыДоставки.СиламиПоставщикаДоНашегоСклада;
		ДокументПрихода.Контрагент = ТТН.Контрагент;
		ДокументПрихода.Партнер = ТТН.Контрагент.Партнер;
		ДокументПрихода.Договор = мЗакупкиСервер.ПолучитьДоговорПоУмолчанию(ДокументПрихода, ДокументПрихода.ХозяйственнаяОперация); 
	
		ДокументПрихода.ЗаполнитьУсловияЗакупокПоУмолчанию();
		ИнициализироватьДокументПриобретение(ДокументПрихода);

		Для Каждого СтрТовар Из СтрокиТовары Цикл
			Если ЗначениеЗаполнено(СтрТовар.Номенклатура) Тогда
				СтрПриход = ДокументПрихода.Товары.Добавить();
				СтрПриход.Номенклатура = СтрТовар.Номенклатура;
				СтрПриход.Склад = ДокументПрихода.Склад;
				СтрПриход.КоличествоУпаковок = РассчитатьКоличествоПрихода(СтрТовар);
				СтрПриход.Количество = СтрПриход.КоличествоУпаковок;
				ЗаполнитьСтрокуПриобретения(СтрПриход, ДокументПрихода, СтрТовар);
			Иначе
				кб99_ВСД.СообщитьИнфо(СтрШаблон("В строке %1 не указана номенклатура, установите соответствие", СтрТовар.НомерСтроки));
			КонецЕсли;
		КонецЦикла;
		
		ДокументПрихода.ЗаполнитьЭтапыГрафикаОплаты();
		ДокументПрихода.Записать();
		
		СформированныеДокументы.Добавить(ДокументПрихода.Ссылка);
		
		кб99_ВСД.СообщитьИнфо(СтрШаблон("Записан документ %1", ДокументПрихода.Ссылка), ДокументПрихода.Ссылка);
	КонецЦикла;
	
	Возврат СформированныеДокументы;
	
КонецФункции

&НаКлиенте
Процедура ОформитьЗакупкуУПоставщика(Команда)
	
	Если Не ПроверитьВозможностьОформленияПоступления() Тогда
		кб99_ВСД.СообщитьИнфо("Не удалось оформить документ ""Приобретение товаров и услуг"" в вашей конфигурации, обратитесь в техническую поддержку");	
		Возврат;	
	КонецЕсли;
	
	ОформленныеДокументы = ОформитьЗакупкуУПоставщикаНаСервере();
	Если ОформленныеДокументы.Количество() Тогда
		Для Каждого Док Из ОформленныеДокументы Цикл
			ОткрытьФорму("Документ.ПриобретениеТоваровУслуг.Форма.ФормаДокумента", Новый Структура("Ключ", Док));		
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПослеЗаполненияАктаНесоответствия(ДанныеЗаполнения, ДополнительныеПараметры = Неопределено) Экспорт

	Если Не ДанныеЗаполнения = Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		ОтправкаЗапросовНаГашение();
	Иначе
		флАктНесоответствия = Ложь;
		тОписаниеНесоответствия = "";
		тПричинаАкта = "";
	КонецЕсли;
	
КонецПроцедуры