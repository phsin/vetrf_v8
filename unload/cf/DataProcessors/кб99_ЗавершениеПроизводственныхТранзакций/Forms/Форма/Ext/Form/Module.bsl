
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСписокНеЗавершенныхТранзакций();
	
КонецПроцедуры

&НаСервере
 Процедура ЗаполнитьСписокНеЗавершенныхТранзакций()
	
	СписокДокументов.Очистить();
		
	Запрос = Новый Запрос("ВЫБРАТЬ
      |	кб_НезавершенныеПроизводственныеТранзакцииСрезПоследних.НомерОперацииНезавершенногоПроизводства КАК НомерОперацииНезавершенногоПроизводства,
      |	кб_НезавершенныеПроизводственныеТранзакцииСрезПоследних.Площадка КАК Площадка,
      |	МИНИМУМ(кб_НезавершенныеПроизводственныеТранзакцииСрезПоследних.ТранзакцияОткрыта) КАК ТранзакцияОткрыта
      |ПОМЕСТИТЬ втПоследниеСтатусыТранзакций
      |ИЗ
      |	РегистрСведений.кб_НезавершенныеПроизводственныеТранзакции.СрезПоследних(, ) КАК кб_НезавершенныеПроизводственныеТранзакцииСрезПоследних
      |
      |СГРУППИРОВАТЬ ПО
      |	кб_НезавершенныеПроизводственныеТранзакцииСрезПоследних.НомерОперацииНезавершенногоПроизводства,
      |	кб_НезавершенныеПроизводственныеТранзакцииСрезПоследних.Площадка
      |;
      |
      |////////////////////////////////////////////////////////////////////////////////
      |ВЫБРАТЬ
      |	втПоследниеСтатусыТранзакций.НомерОперацииНезавершенногоПроизводства КАК НомерОперации,
      |	втПоследниеСтатусыТранзакций.Площадка КАК Площадка
      |ПОМЕСТИТЬ втНезавершенныеТранзакции
      |ИЗ
      |	втПоследниеСтатусыТранзакций КАК втПоследниеСтатусыТранзакций
      |ГДЕ
      |	втПоследниеСтатусыТранзакций.ТранзакцияОткрыта
      |
      |ИНДЕКСИРОВАТЬ ПО
      |	НомерОперации,
      |	Площадка
      |;
      |
      |////////////////////////////////////////////////////////////////////////////////
      |ВЫБРАТЬ
      |	втНезавершенныеТранзакции.НомерОперации КАК НомерОперации,
      |	втНезавершенныеТранзакции.Площадка КАК Площадка,
      |	кб_НезавершенныеПроизводственныеТранзакцииСрезПоследних.ТранзакцияОткрыта КАК Отметка,
      |	кб_НезавершенныеПроизводственныеТранзакцииСрезПоследних.Период КАК Период,
      |	кб_НезавершенныеПроизводственныеТранзакцииСрезПоследних.ПродукцияЭлемент КАК ПродукцияЭлемент,
      |	кб_НезавершенныеПроизводственныеТранзакцииСрезПоследних.ДатаПроизводства КАК ДатаПроизводства,
      |	кб_НезавершенныеПроизводственныеТранзакцииСрезПоследних.Организация КАК Организация
      |ИЗ
      |	втНезавершенныеТранзакции КАК втНезавершенныеТранзакции
      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.кб_НезавершенныеПроизводственныеТранзакции.СрезПоследних(
      |				,
      |				(НомерОперацииНезавершенногоПроизводства, Площадка) В
      |						(ВЫБРАТЬ
      |							СписокНезавершенных.НомерОперации,
      |							СписокНезавершенных.Площадка
      |						ИЗ
      |							втНезавершенныеТранзакции КАК СписокНезавершенных)
      |					И ТранзакцияОткрыта) КАК кб_НезавершенныеПроизводственныеТранзакцииСрезПоследних
      |		ПО втНезавершенныеТранзакции.НомерОперации = кб_НезавершенныеПроизводственныеТранзакцииСрезПоследних.НомерОперацииНезавершенногоПроизводства
      |			И (кб_НезавершенныеПроизводственныеТранзакцииСрезПоследних.ТранзакцияОткрыта)
      |			И втНезавершенныеТранзакции.Площадка = кб_НезавершенныеПроизводственныеТранзакцииСрезПоследних.Площадка
      |ИТОГИ
      |	МИНИМУМ(Отметка),
      |	МАКСИМУМ(Период),
      |	МАКСИМУМ(ДатаПроизводства),
      |	МАКСИМУМ(Организация)
      |ПО
      |	Площадка,
      |	НомерОперации");
  	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаПоПлощадкам = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоПлощадкам.Следующий() Цикл
		
		ВыборкаПоДокументам = ВыборкаПоПлощадкам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПоДокументам.Следующий() Цикл
			нСтр = СписокДокументов.Добавить();
			нСтр.НомерОперации = ВыборкаПоДокументам.НомерОперации;
			нСтр.Отметка = ВыборкаПоДокументам.Отметка;
			нСтр.Площадка = ВыборкаПоДокументам.Площадка;
			нСтр.Период = ВыборкаПоДокументам.Период;
			нСтр.Организация = ВыборкаПоДокументам.Организация;
			ВыборкаПоПродукции = ВыборкаПоДокументам.Выбрать();
			Пока ВыборкаПоПродукции.Следующий() Цикл
				
				СтруктураСтроки = Новый Структура("ПродукцияЭлемент, ДатаПроизводства");
				ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаПоПродукции);
				
				нСтр.СписокПродукции.Добавить(СтруктураСтроки);
				
			КонецЦикла;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура кнЗавершитьВыбранныеТранзакции(Команда)
	
	ПредставлениеДокументыКЗавершению = "";
	
	Для Каждого Стр Из СписокДокументов Цикл
		Если Стр.Отметка Тогда
			ПредставлениеДокументыКЗавершению = ПредставлениеДокументыКЗавершению +", "+ Стр.НомерОперации;
		КонецЕсли;
	КонецЦикла;
	
	ПервыйСимвол = Лев(ПредставлениеДокументыКЗавершению, 1);
	Если ПервыйСимвол = "," Тогда
		ПредставлениеДокументыКЗавершению = Прав(ПредставлениеДокументыКЗавершению, СтрДлина(ПредставлениеДокументыКЗавершению)-2);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПредставлениеДокументыКЗавершению) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = "Будут завершены производственные транзакции №: "+ПредставлениеДокументыКЗавершению+"
					| Продолжить?";
	
	Оповещение = Новый ОписаниеОповещения("кнЗавершитьПроизводствоОтвет", ЭтаФорма);	
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,  0, КодВозвратаДиалога.Да, ""   );
	
КонецПроцедуры

&НаСервере
Функция кнЗавершитьВыбранныеТранзакцииНаСервере()
		
	Для Каждого СтрТЧ Из СписокДокументов Цикл
		Если СтрТЧ.Отметка Тогда
			
			ПараметрыОрганизации = кб99_ВСД.ЗагрузитьПараметры( СтрТЧ.Организация );
		   	ПараметрыОрганизации.Вставить( "ЗавершитьПроизводство", Истина );

			ПараметрыЗаполнения = Новый Структура;
			ПараметрыЗаполнения.Вставить("ПараметрыОрганизации", ПараметрыОрганизации);
			ПараметрыЗаполнения.Вставить("Площадка", СтрТЧ.Площадка);
			ПараметрыЗаполнения.Вставить("НомерОперации", СтрТЧ.НомерОперации);
			ПараметрыЗаполнения.Вставить("СписокПродукции", СтрТЧ.СписокПродукции.ВыгрузитьЗначения());
			
			СтрТЧ.Документ = кб99_ВСД_Производство.ОформитьДокументЗавершенияПроизводства(ПараметрыЗаполнения);
			СтрТЧ.СтатусЗапроса = кб99_ВСД_Запросы.ЗавершитьПроизводство( ПараметрыОрганизации, СтрТЧ.Документ );
			
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

&НаКлиенте
Процедура кнЗавершитьПроизводствоОтвет(Ответ, ДопПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		кнЗавершитьВыбранныеТранзакцииНаСервере();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЗаполнитьСписокНеЗавершенныхТранзакций();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	
	ЗаполнитьСписокНеЗавершенныхТранзакций();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметки(Команда)
	
	Флажок = Команда.Имя = "ОтметитьВсе";
	ОтметитьСтроки(Флажок);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьСтроки(Флажок)
	
	Для Каждого Стр Из СписокДокументов Цикл
		Стр.Отметка = Флажок;	
	КонецЦикла;
	
КонецПроцедуры

