
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ДокСсылка = ПараметрКоманды;
	Организация = кб99_ВСД.ПолучитьЗначениеРевизитаОбъекта_НаСервере( ДокСсылка, "Организация" );
	ПараметрыОрганизации = кб99_ВСД.ЗагрузитьПараметры( Организация );
	Если ПараметрыОрганизации["ПарамРазрешитьВводНаОснованииБолееОдногоВСД"] Тогда 
		мсТермУсловияПеревозки = ПреобразоватьТаблицуЗначенийВМассив(ДокСсылка); 
		Если мсТермУсловияПеревозки.Количество() > 1 Тогда
			Парам = Новый Структура("ПараметрКоманды,ПараметрыВыполненияКоманды, мсТермУсловияПеревозки, ПараметрыОрганизации",ПараметрКоманды,ПараметрыВыполненияКоманды,мсТермУсловияПеревозки,ПараметрыОрганизации);
			Оповещение = Новый ОписаниеОповещения("ОбработкаОтвета",ЭтотОбъект, Парам);	
			ПоказатьВопрос(Оповещение, "В документе продукция с различными термическими условиями перевозки, выбрать терм.условия?", РежимДиалогаВопрос.ДаНет,  0, КодВозвратаДиалога.Да, ""   );
		КонецЕсли;
	Иначе
		// только 1 ВСД
		ДокВСД = кб99_ВСД.НайтиВсдТранзакцию(ДокСсылка);
		Если ЗначениеЗаполнено(ДокВСД) Тогда
			Если типЗнч(ДокВСД) = Тип("ДокументСсылка.ВСД2_транзакция") Тогда
				ИмяФормыДокумента = "Документ.ВСД2_транзакция.ФормаОбъекта";	
			ИначеЕсли типЗнч(ДокВСД) = Тип("ДокументСсылка.ВСД_транзакция") Тогда
				ИмяФормыДокумента = "Документ.ВСД_транзакция.ФормаОбъекта";
			Иначе
				кб99_ВСД.СообщитьИнфо("Неверный документ нашли", ДокВСД);
				Возврат;
			КонецЕсли;
			ОткрытьФорму(ИмяФормыДокумента, Новый Структура("Ключ", ДокВСД));
		Иначе
			ОткрытьФорму("Документ.ВСД2_транзакция.ФормаОбъекта", Новый Структура("Основание", ДокСсылка));
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтвета(Ответ, Парам) Экспорт
	
	ДокСсылка = Парам.ПараметрКоманды;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		СписокВыбора = Новый СписокЗначений;
		Для Каждого ТермУсловие Из Парам.мсТермУсловияПеревозки Цикл
			СписокВыбора.Добавить(ТермУсловие);
		КонецЦикла;
		СтруктураПараметров = Новый Структура("СписокВыбора",СписокВыбора);
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОформитьВсдПоВыбТермУсловиям", ЭтотОбъект, Парам);
		ОткрытьФорму("ОбщаяФорма.кб99_ФормаТермическиеУсловия", СтруктураПараметров,,,,, ОповещениеОЗакрытии);
	Иначе
		ОткрытьФорму("Документ.ВСД2_транзакция.ФормаОбъекта", Новый Структура("Основание", ДокСсылка));
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОформитьВсдПоВыбТермУсловиям(РезультатЗакрытия, Парам) Экспорт
	
	ДокСсылка =   Парам.ПараметрКоманды;
	Основание = Новый Структура("Док, выбТермУсловие", ДокСсылка,РезультатЗакрытия);
	Если ЗначениеЗаполнено(РезультатЗакрытия) И (ТипЗнч(РезультатЗакрытия) <> Тип("КодВозвратаДиалога")) Тогда
		Док = СоздатьВсдПоВыбТермУсловию(Основание);
		ОткрытьФорму("Документ.ВСД2_транзакция.Форма.ФормаДокумента", Новый Структура("Ключ", Док));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПреобразоватьТаблицуЗначенийВМассив(Док) Экспорт
	
	тзДанные = кб99_ВСД_Общий.ВыгрузитьТч(Док);
	тзДанные.Свернуть("ТермУсловияПеревозки");  
	мсДанные = Новый Массив;
	
	// Запишем в массив
	Для Каждого СтрокаТЗ Из тзДанные Цикл
		мсДанные.Добавить(СтрокаТЗ.ТермУсловияПеревозки);
	КонецЦикла;
	
	Возврат мсДанные;
	
КонецФункции 

&НаСервере
Функция СоздатьВсдПоВыбТермУсловию(Основание)   Экспорт
	
	Док = Основание.Док;
	выбТермУсловие = Основание.ВыбТермУсловие;
	ПараметрыОрганизации = кб99_ВСД.ЗагрузитьПараметры( Док.Организация );	
	тзПартий = кб99_ВСД_Общий.ВыгрузитьТч(Док);
	Отбор = Новый Структура("ТермУсловияПеревозки", выбТермУсловие);
	тзЭлементыКсписанию = тзПартий.Скопировать(Отбор);
	тзАктуальныхПартий = кб99_ВСД.тзПартииСписанияПоТзПродукция_Элемент(ПараметрыОрганизации,тзЭлементыКсписанию);
	
	Если ПараметрыОрганизации["ПарамФильтроватьРеализациюПоСкладуПлощадкиОтправителя"] Тогда   // Определим нашу площадку по Складу
		_Отправитель_Площадка = кб99_ВСД.НайтиПлощадкуПоСкладу(Док.Склад, ПараметрыОрганизации["Отправитель_ХозСубъект"]);
	Иначе
		_Отправитель_Площадка = ПараметрыОрганизации["Отправитель_Площадка"];
	КонецЕсли;
	_Получатель_ХозСубъект = кб99_ВСД.НайтиХозСубъект(Док.Контрагент);				
	Если ПараметрыОрганизации["РеквизитГрузополучатель"] = 0 Тогда 
		//контрагент
		_Получатель_Площадка = кб99_ВСД.НайтиПлощадкуПоКонтрагенту(Док.Контрагент);
	Иначе
		//Адрес доставки
		_Получатель_Площадка = кб99_ВСД.НайтиПлощадкуПоКонтрагенту(Док.АдресДоставки);
	КонецЕсли;
	
	ПараметрыДокумента = Новый Структура;
	ПараметрыДокумента.Вставить("Получатель_ХозСубъект", _Получатель_ХозСубъект );
	ПараметрыДокумента.Вставить("Получатель_Площадка", _Получатель_Площадка );
	ПараметрыДокумента.Вставить("Отправитель_Площадка", _Отправитель_Площадка );
	ПараметрыДокумента.Вставить("ДокОснование", Док);
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("ПараметрыДокумента", ПараметрыДокумента);
	ПараметрыЗаполнения.Вставить("ДокументОснование", Док);
	ПараметрыЗаполнения.Вставить("Партии", тзАктуальныхПартий);
		
	ДокВСД = Документы.ВСД2_Транзакция.СоздатьДокумент();
	ДокВСД.Заполнить( ПараметрыЗаполнения );
	ДокВСД.ТермическиеУсловияПеревозки =  выбТермУсловие;
	ДокВСД.Записать();
	кб99_ВСД.СообщитьИнфо(" Создан документ "+ДокВСД+ " по выбранным термическим условиям перевозки: " + выбТермУсловие);		
	Возврат ДокВСД.Ссылка;
	
КонецФункции
