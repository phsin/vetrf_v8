&НаКлиенте
Перем мПредставлениеПустогоРасписания;

#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РегламентноеЗадание_кб99_ВСД_УдалитьЗапросы 	  = СоздатьРегламентноеЗаданиеПриНеобходимости("кб99_ВСД_УдалитьЗапросы");
	РегламентноеЗадание_кб99_ВСД_УдалитьЗапросы_XML   = СоздатьРегламентноеЗаданиеПриНеобходимости("кб99_ВСД_УдалитьЗапросы_XML");
	РегламентноеЗадание_кб99_ПолучитьАктуальныеПартии =  СоздатьРегламентноеЗаданиеПриНеобходимости("кб99_ПолучитьАктуальныеПартии");
	РегламентноеЗадание_кб99_ОбновитьСтатусыВСД 	  =  СоздатьРегламентноеЗаданиеПриНеобходимости("кб99_ОбновитьСтатусыВСД");
	
	ИспользованиеОчиститьРС =  РегламентноеЗадание_кб99_ВСД_УдалитьЗапросы.Использование;
	РасписаниеРегламентногоЗаданияКб99_ВСД_УдалитьЗапросы =  РегламентноеЗадание_кб99_ВСД_УдалитьЗапросы.Расписание;
	ИнтервалПовтораПриАварийномЗавершенииОчиститьРС = РегламентноеЗадание_кб99_ВСД_УдалитьЗапросы.ИнтервалПовтораПриАварийномЗавершении;
	КоличествоПовторовПриАварийномЗавершенииОчиститьРС_Запросы = РегламентноеЗадание_кб99_ВСД_УдалитьЗапросы.КоличествоПовторовПриАварийномЗавершении;
	
	ИспользованиеОчиститьВнешФайлы = РегламентноеЗадание_кб99_ВСД_УдалитьЗапросы_XML.Использование;
	РасписаниеРегламентногоЗаданияКБ99_ОчиститьВнешнююПапкуСЛогФайлами = РегламентноеЗадание_кб99_ВСД_УдалитьЗапросы_XML.Расписание; 
	ИнтервалПовтораПриАварийномЗавершенииОчиститьВнешнююПапкуСЛогФайлами = РегламентноеЗадание_кб99_ВСД_УдалитьЗапросы_XML.ИнтервалПовтораПриАварийномЗавершении;
	КоличествоПовторовПриАварийномЗавершенииОчиститьВнешнююПапкуСЛогФайлами = РегламентноеЗадание_кб99_ВСД_УдалитьЗапросы_XML.КоличествоПовторовПриАварийномЗавершении;
	
	ИспользованиеЗагружатьПартии = РегламентноеЗадание_кб99_ПолучитьАктуальныеПартии.Использование;
	РасписаниеРегламентногоЗаданияКб99_ПолучитьАктуальныеПартии = РегламентноеЗадание_кб99_ПолучитьАктуальныеПартии.Расписание;
	МассивПараметров = РегламентноеЗадание_кб99_ПолучитьАктуальныеПартии.Параметры;
	Если ТипЗнч(МассивПараметров) = Тип("Массив") И МассивПараметров.Количество() > 0 Тогда
		МассивНастроек = МассивПараметров[0];
		Для Каждого Настройка Из МассивНастроек Цикл
			
			Площадка = Настройка.Значение;
			
		КонецЦикла;
	КонецЕсли;
	ИнтервалПовтораПриАварийномЗавершенииПолучитьАктуальныеПартии = РегламентноеЗадание_кб99_ПолучитьАктуальныеПартии.ИнтервалПовтораПриАварийномЗавершении;
	КоличествоПовторовПриАварийномЗавершенииПолучитьАктуальныеПартии = РегламентноеЗадание_кб99_ПолучитьАктуальныеПартии.КоличествоПовторовПриАварийномЗавершении;
	
	ИспользованиеОбновитьСтатусыВСД = РегламентноеЗадание_кб99_ОбновитьСтатусыВСД.Использование;
	РасписаниеРегламентногоЗаданияКб99_ОбновитьСтатусыВСД = РегламентноеЗадание_кб99_ОбновитьСтатусыВСД.Расписание;
	МассивПараметров = РегламентноеЗадание_кб99_ОбновитьСтатусыВСД.Параметры;
	Если ТипЗнч(МассивПараметров) = Тип("Массив") И МассивПараметров.Количество() > 0 Тогда
		ОбновлятьСтатусыЗа = МассивПараметров[0].КоличествоДней;	
	КонецЕсли;
	ИнтервалПовтораПриАварийномЗавершенииОбновитьСтатусыВСД = РегламентноеЗадание_кб99_ОбновитьСтатусыВСД.ИнтервалПовтораПриАварийномЗавершении;
	КоличествоПовторовПриАварийномЗавершенииОбновитьСтатусыВСД = РегламентноеЗадание_кб99_ОбновитьСтатусыВСД.КоличествоПовторовПриАварийномЗавершении;
	
	КлючОбъекта = "ОбщаяФорма.кб99_РегламентноеЗаданиеГашение";
	Попытка
		СписокЗаданийГашениеИзХранилища = ХранилищеОбщихНастроек.Загрузить(КлючОбъекта, "СписокЗаданийГашение",, ИмяПользователя());
		Если СписокЗаданийГашениеИзХранилища<>Неопределено Тогда
			Для Каждого Задание Из СписокЗаданийГашениеИзХранилища Цикл
				стрЗадание = СписокЗаданийГашение.Добавить();
				ЗаполнитьЗначенияСвойств(стрЗадание, Задание);
			КонецЦикла;
		КонецЕсли;
	Исключение
		кб99_ВСД.СообщитьИнфо("Не удалось получить список регламентных заданий по гашению входящих ВСД"+Символы.ПС+ОписаниеОшибки());
	КонецПопытки;

	Попытка
		СписокЗаданийОтправкаВСДИзХранилища = ХранилищеОбщихНастроек.Загрузить(КлючОбъекта, "СписокЗаданийОтправкаВСДТранзакции",, ИмяПользователя());
		Если СписокЗаданийОтправкаВСДИзХранилища <> Неопределено Тогда
			Для Каждого Задание Из СписокЗаданийОтправкаВСДИзХранилища Цикл
				стрЗадание = СписокЗаданийОтправкаВСДТранзакции.Добавить();
				ЗаполнитьЗначенияСвойств(стрЗадание, Задание);
			КонецЦикла;
		КонецЕсли;
	Исключение
		кб99_ВСД.СообщитьИнфо("Не удалось получить список регламентных заданий по отправке ВСД2 Транзакции"+Символы.ПС+ОписаниеОшибки());
	КонецПопытки;

	Попытка
		СписокЗаданийПроизводствоИзХранилища = ХранилищеОбщихНастроек.Загрузить(КлючОбъекта, "СписокЗаданийПроизводство",, ИмяПользователя());
		Если СписокЗаданийПроизводствоИзХранилища <> Неопределено Тогда
			Для Каждого Задание Из СписокЗаданийПроизводствоИзХранилища Цикл
				стрЗадание = СписокЗаданийПроизводство.Добавить();
				ЗаполнитьЗначенияСвойств(стрЗадание, Задание);
			КонецЦикла;
		КонецЕсли;
	Исключение
		кб99_ВСД.СообщитьИнфо("Не удалось получить список регламентных заданий по обработке производства"+Символы.ПС+ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	мПредставлениеПустогоРасписания = Строка(Новый РасписаниеРегламентногоЗадания);
	ОбновитьПредставлениеРасписания();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_РегламентныеЗаданияГашение" Тогда
		СтруктураОтбора = Новый Структура("Организация, Идентификатор", Параметр.Организация, Параметр.Идентификатор);
		НайденныеСтроки = СписокЗаданийГашение.НайтиСтроки(СтруктураОтбора);
		Если НайденныеСтроки.Количество() = 0 Тогда
			НовСтрока = СписокЗаданийГашение.Добавить();
		Иначе
			НовСтрока = НайденныеСтроки[0];
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(новСтрока, Параметр);
		Модифицированность = Истина;
	ИначеЕсли ИмяСобытия = "Запись_РегламентныеЗаданияОтпарвкаВСДТранзакция" Тогда
		СтруктураОтбора = Новый Структура("Организация, Идентификатор", Параметр.Организация, Параметр.Идентификатор);
		НайденныеСтроки = СписокЗаданийОтправкаВСДТранзакции.НайтиСтроки(СтруктураОтбора);
		Если НайденныеСтроки.Количество() = 0 Тогда
			НовСтрока = СписокЗаданийОтправкаВСДТранзакции.Добавить();
		Иначе
			НовСтрока = НайденныеСтроки[0];
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(новСтрока, Параметр);
		Модифицированность = Истина;
	ИначеЕсли ИмяСобытия = "Запись_РегламентныеЗаданияОбработкаПроизводства" Тогда
		СтруктураОтбора = Новый Структура("Организация, Идентификатор", Параметр.Организация, Параметр.Идентификатор);
		НайденныеСтроки = СписокЗаданийПроизводство.НайтиСтроки(СтруктураОтбора);
		Если НайденныеСтроки.Количество() = 0 Тогда
			НовСтрока = СписокЗаданийПроизводство.Добавить();
		Иначе
			НовСтрока = НайденныеСтроки[0];
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(новСтрока, Параметр);
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Модифицированность Тогда
		Если ЗаписатьПараметрыРегламентногоЗадания() Тогда
			Закрыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Сохранить(Команда)

	Если ЗаписатьПараметрыРегламентногоЗадания() Тогда
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ЗаписатьПараметрыРегламентногоЗадания()
	
	НайденыОшибки = Ложь;
	Если ИспользованиеЗагружатьПартии И Площадка.Количество() = 0 Тогда
		ТекстОшибки = НСтр("ru = 'Не выбраны площадки'");
		кб99_ВСД.СообщитьПользователю(ТекстОшибки,,"Площадка");
		НайденыОшибки = Истина;
	ИначеЕсли ИспользованиеОбновитьСтатусыВСД И Не ЗначениеЗаполнено(ОбновлятьСтатусыЗа) Тогда
		ТекстОшибки = НСтр("ru = 'Не указан период обновления статусов ВСД'");
		кб99_ВСД.СообщитьПользователю(ТекстОшибки,,"ОбновлятьСтатусыЗа");
		НайденыОшибки = Истина;
	КонецЕсли;
	
	Если НайденыОшибки Тогда
		Возврат Ложь;
	КонецЕсли;
	
	//Очистить регистр сведений кб99_Запросы
	РегламентноеЗаданиеОбъект = СоздатьРегламентноеЗаданиеПриНеобходимости("кб99_ВСД_УдалитьЗапросы");
	РегламентноеЗаданиеОбъект.Расписание = РасписаниеРегламентногоЗаданияКб99_ВСД_УдалитьЗапросы;
	РегламентноеЗаданиеОбъект.Использование = ИспользованиеОчиститьРС;
	РегламентноеЗаданиеОбъект.КоличествоПовторовПриАварийномЗавершении = КоличествоПовторовПриАварийномЗавершенииОчиститьРС_Запросы;
	РегламентноеЗаданиеОбъект.ИнтервалПовтораПриАварийномЗавершении = ИнтервалПовтораПриАварийномЗавершенииОчиститьРС;
	РегламентноеЗаданиеОбъект.Записать();
	
	//Очистить внешнюю папку с лог Файлами
	РегламентноеЗаданиеОбъект = СоздатьРегламентноеЗаданиеПриНеобходимости("кб99_ВСД_УдалитьЗапросы_XML");
	РегламентноеЗаданиеОбъект.Расписание = РасписаниеРегламентногоЗаданияКБ99_ОчиститьВнешнююПапкуСЛогФайлами;
	РегламентноеЗаданиеОбъект.Использование = ИспользованиеОчиститьВнешФайлы;
	РегламентноеЗаданиеОбъект.КоличествоПовторовПриАварийномЗавершении = КоличествоПовторовПриАварийномЗавершенииОчиститьВнешнююПапкуСЛогФайлами;
	РегламентноеЗаданиеОбъект.ИнтервалПовтораПриАварийномЗавершении = ИнтервалПовтораПриАварийномЗавершенииОчиститьВнешнююПапкуСЛогФайлами;
	РегламентноеЗаданиеОбъект.Записать();
	
	//ПолучитьАктуальныеПартии
	МассивПараметровЗагрузкаПартий = Новый Массив;
	СтруктураНастройки = Новый Структура();
	СтруктураНастройки.Вставить("Площадки"	, Площадка);
	МассивПараметровЗагрузкаПартий.Добавить(СтруктураНастройки);
	
	РегламентноеЗаданиеОбъект =  СоздатьРегламентноеЗаданиеПриНеобходимости("кб99_ПолучитьАктуальныеПартии");
	РегламентноеЗаданиеОбъект.Использование = ИспользованиеЗагружатьПартии;
	РегламентноеЗаданиеОбъект.Расписание = РасписаниеРегламентногоЗаданияКб99_ПолучитьАктуальныеПартии;
	РегламентноеЗаданиеОбъект.ИнтервалПовтораПриАварийномЗавершении = ИнтервалПовтораПриАварийномЗавершенииПолучитьАктуальныеПартии;
	РегламентноеЗаданиеОбъект.КоличествоПовторовПриАварийномЗавершении = КоличествоПовторовПриАварийномЗавершенииПолучитьАктуальныеПартии;
	РегламентноеЗаданиеОбъект.Параметры = МассивПараметровЗагрузкаПартий;
	РегламентноеЗаданиеОбъект.Записать();
	
	//Обновление статусов ВСД
	МассивПараметровОбновлениеСтатусовВСД = Новый Массив;
	Параметр = Новый Структура();
	Параметр.Вставить("КоличествоДней", ОбновлятьСтатусыЗа);
	МассивПараметровОбновлениеСтатусовВСД.Добавить(Параметр);
	
	РегламентноеЗаданиеОбъект =  СоздатьРегламентноеЗаданиеПриНеобходимости("кб99_ОбновитьСтатусыВСД");
	РегламентноеЗаданиеОбъект.Использование = ИспользованиеОбновитьСтатусыВСД;
	РегламентноеЗаданиеОбъект.Расписание = РасписаниеРегламентногоЗаданияКб99_ОбновитьСтатусыВСД;
	РегламентноеЗаданиеОбъект.ИнтервалПовтораПриАварийномЗавершении = ИнтервалПовтораПриАварийномЗавершенииОбновитьСтатусыВСД;
	РегламентноеЗаданиеОбъект.КоличествоПовторовПриАварийномЗавершении = КоличествоПовторовПриАварийномЗавершенииОбновитьСтатусыВСД;
	РегламентноеЗаданиеОбъект.Параметры = МассивПараметровОбновлениеСтатусовВСД;
	РегламентноеЗаданиеОбъект.Записать();
	
	МассивЗаданий = Новый Массив;
	Для Каждого Стр Из СписокЗаданийГашение Цикл
		СтруктураЗадания = Новый Структура("Использование, Организация, Наименование, Расписание, Идентификатор");
		ЗаполнитьЗначенияСвойств(СтруктураЗадания, Стр);
		МассивЗаданий.Добавить(СтруктураЗадания);
	КонецЦикла;
	
	КлючОбъекта = "ОбщаяФорма.кб99_РегламентноеЗаданиеГашение";
	Попытка
		ХранилищеОбщихНастроек.Сохранить(КлючОбъекта,  "СписокЗаданийГашение", МассивЗаданий,, ИмяПользователя());
	Исключение
		кб99_ВСД.СообщитьИнфо("Не удалось сохранить данные по регламентным заданиям ""Гашение входящих ВСД"""+Символы.ПС+ОписаниеОшибки());	
	КонецПопытки;

	МассивЗаданий = Новый Массив;
	Для Каждого Стр Из СписокЗаданийОтправкаВСДТранзакции Цикл
		СтруктураЗадания = Новый Структура("Использование, Организация, Наименование, Расписание, Идентификатор");
		ЗаполнитьЗначенияСвойств(СтруктураЗадания, Стр);
		МассивЗаданий.Добавить(СтруктураЗадания);
	КонецЦикла;
	
	Попытка
		ХранилищеОбщихНастроек.Сохранить(КлючОбъекта,  "СписокЗаданийОтправкаВСДТранзакции", МассивЗаданий,, ИмяПользователя());
	Исключение
		кб99_ВСД.СообщитьИнфо("Не удалось сохранить данные по регламентным заданиям ""Отправка ВСД Транзакции"""+Символы.ПС+ОписаниеОшибки());	
	КонецПопытки;
	
	МассивЗаданий = Новый Массив;
	Для Каждого Стр Из СписокЗаданийПроизводство Цикл
		СтруктураЗадания = Новый Структура("Использование, Организация, Наименование, Расписание, Идентификатор");
		ЗаполнитьЗначенияСвойств(СтруктураЗадания, Стр);
		МассивЗаданий.Добавить(СтруктураЗадания);
	КонецЦикла;
	
	Попытка
		ХранилищеОбщихНастроек.Сохранить(КлючОбъекта,  "СписокЗаданийПроизводство", МассивЗаданий,, ИмяПользователя());
	Исключение
		кб99_ВСД.СообщитьИнфо("Не удалось сохранить данные по регламентным заданиям ""обработка производства"""+Символы.ПС+ОписаниеОшибки());	
	КонецПопытки;
	
	Модифицированность = Ложь;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция НайтиРегламентноеЗадание(НаименованиеРегламентногоЗадания)
	
	РегламентноеЗаданиеОбъект = Неопределено;
	
	Отбор = Новый Структура("Наименование", НаименованиеРегламентногоЗадания);
	МассивЗаданий = РегламентныеЗадания.ПолучитьРегламентныеЗадания(Отбор);
	Если МассивЗаданий.Количество() <> 0 Тогда
		РегламентноеЗаданиеОбъект = МассивЗаданий[0];
	КонецЕсли;
	
	Возврат РегламентноеЗаданиеОбъект;
	
КонецФункции

&НаСервере
Функция СоздатьРегламентноеЗаданиеПриНеобходимости(НаименованиеРегламентногоЗадания)
	
	РегламентноеЗаданиеОбъект = НайтиРегламентноеЗадание(НаименованиеРегламентногоЗадания);
	
	// при необходимости создаем регл. задание
	Если РегламентноеЗаданиеОбъект = Неопределено Тогда
		РегламентноеЗаданиеОбъект = РегламентныеЗадания.СоздатьРегламентноеЗадание(НаименованиеРегламентногоЗадания);
		РегламентноеЗаданиеОбъект.Наименование = НаименованиеРегламентногоЗадания;
	КонецЕсли;
	
	Возврат РегламентноеЗаданиеОбъект;
	
КонецФункции

&НаКлиенте
Процедура НастроитьРасписание(Команда)
	
	ИмяРасписания = ""+Команда.Имя;
	
	Если ИмяРасписания = "НастроитьРасписаниекб99_ВСД_УдалитьЗапросы" Тогда
		РасписаниеРегламентногоЗадания = РасписаниеРегламентногоЗаданияКб99_ВСД_УдалитьЗапросы;
	ИначеЕсли ИмяРасписания = "НастроитьРасписаниекб99_ОчиститьВнешнююПапкуСлогФайлами" Тогда
		РасписаниеРегламентногоЗадания = РасписаниеРегламентногоЗаданияКБ99_ОчиститьВнешнююПапкуСЛогФайлами;
	ИначеЕсли ИмяРасписания = "НастроитьРасписаниеКб99_ОбновитьСтатусыВСД" Тогда
		РасписаниеРегламентногоЗадания = РасписаниеРегламентногоЗаданияКб99_ОбновитьСтатусыВСД;
	ИначеЕсли ИмяРасписания = "НастроитьРасписаниеКб99_ПолучитьАктуальныеПартии" Тогда
		РасписаниеРегламентногоЗадания = РасписаниеРегламентногоЗаданияКб99_ПолучитьАктуальныеПартии;
	КонецЕсли;

	РедактированиеРасписаниеРегламентногоЗадания(ИмяРасписания,РасписаниеРегламентногоЗадания);

КонецПроцедуры

&НаКлиенте
Процедура РедактированиеРасписаниеРегламентногоЗадания(ИмяРасписания,РасписаниеРегламентногоЗадания)
	
	
	// если расписание не инициализировано в форме на сервере, то создаем новое
	Если РасписаниеРегламентногоЗадания = Неопределено Тогда
		РасписаниеРегламентногоЗадания = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеРегламентногоЗадания);
	
	ПараметрыФункции = Новый Структура("ИмяРасписания", ИмяРасписания);
	// открываем диалог для редактирования Расписания
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"РедактированиеРасписанияРегламентногоЗаданияПродолжение",       
		ЭтаФорма, ПараметрыФункции);  
		
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеРасписанияРегламентногоЗаданияПродолжение (Расписание, ПараметрыФункции) Экспорт
	
	Если Расписание <> Неопределено Тогда 
		Если  ПараметрыФункции.ИмяРасписания = "НастроитьРасписаниекб99_ВСД_УдалитьЗапросы" Тогда
			РасписаниеРегламентногоЗаданияКб99_ВСД_УдалитьЗапросы = Расписание;	
		ИначеЕсли ПараметрыФункции.ИмяРасписания = "НастроитьРасписаниекб99_ОчиститьВнешнююПапкуСлогФайлами" Тогда 
			РасписаниеРегламентногоЗаданияКБ99_ОчиститьВнешнююПапкуСЛогФайлами = Расписание;
		ИначеЕсли ПараметрыФункции.ИмяРасписания = "НастроитьРасписаниеКб99_ОбновитьСтатусыВСД" Тогда
			РасписаниеРегламентногоЗаданияКб99_ОбновитьСтатусыВСД = Расписание;
		ИначеЕсли ПараметрыФункции.ИмяРасписания = "НастроитьРасписаниеКб99_ПолучитьАктуальныеПартии" Тогда
			РасписаниеРегламентногоЗаданияКб99_ПолучитьАктуальныеПартии = Расписание;
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьПредставлениеРасписания();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПредставлениеРасписания()
	
	//Очистить РС
	ПредставлениеРасписания = Строка(РасписаниеРегламентногоЗаданияКб99_ВСД_УдалитьЗапросы);
	Если ПредставлениеРасписания = мПредставлениеПустогоРасписания Тогда
		ПредставлениеРасписания = НСтр("ru = 'Расписание не задано'");
	КонецЕсли;
	Элементы.НастроитьРасписаниекб99_ВСД_УдалитьЗапросы.Заголовок = ПредставлениеРасписания;
	
	//ОчиститьВнешнююПапкуСЛогФайлами
	ПредставлениеРасписания = Строка(РасписаниеРегламентногоЗаданияКБ99_ОчиститьВнешнююПапкуСЛогФайлами);
	Если ПредставлениеРасписания = мПредставлениеПустогоРасписания Тогда
		ПредставлениеРасписания = НСтр("ru = 'Расписание не задано'");
	КонецЕсли;
	Элементы.НастроитьРасписаниекб99_ОчиститьВнешнююПапкуСлогФайлами.Заголовок = ПредставлениеРасписания;
	
	//ПолучитьАктуальныеПартии
	ПредставлениеРасписания = Строка(РасписаниеРегламентногоЗаданияКб99_ПолучитьАктуальныеПартии);
	Если ПредставлениеРасписания = мПредставлениеПустогоРасписания Тогда
		ПредставлениеРасписания = НСтр("ru = 'Расписание не задано'");
	КонецЕсли;
	Элементы.НастроитьРасписаниеКб99_ПолучитьАктуальныеПартии.Заголовок = ПредставлениеРасписания;

	//Обновить статусы ВСД
	ПредставлениеРасписания = Строка(РасписаниеРегламентногоЗаданияКб99_ОбновитьСтатусыВСД);
	Если ПредставлениеРасписания = мПредставлениеПустогоРасписания Тогда
		ПредставлениеРасписания = НСтр("ru = 'Расписание не задано'");
	КонецЕсли;
	Элементы.НастроитьРасписаниеКб99_ОбновитьСтатусыВСД.Заголовок = ПредставлениеРасписания;

КонецПроцедуры

#Область Гашение

&НаКлиенте
Процедура ДобавитьСкопироватьИзменитьРегламентноеЗаданиеГашение(Знач Действие)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Действие", Действие);
	
	Если Действие <> "Добавить" Тогда
		Если Элементы.СписокЗаданийГашение.ТекущиеДанные = Неопределено Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Выберите регламентное задание.'"));
			Возврат;
		Иначе
			ПараметрыФормы.Вставить("Идентификатор", Элементы.СписокЗаданийГашение.ТекущиеДанные.Идентификатор);
		КонецЕсли;
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.кб99_РегламентноеЗаданиеГашение", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЗаданиеГашение(Команда)
	
	ДобавитьСкопироватьИзменитьРегламентноеЗаданиеГашение("Добавить");
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаданийГашениеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СписокЗаданийГашениеИспользование"	 
					ИЛИ Поле.Имя = "СписокЗаданийГашениеНаименование"
					ИЛИ Поле.Имя = "СписокЗаданийГашениеРасписание"	
					ИЛИ Поле.Имя = "СписокЗаданийГашениеИдентификатор" Тогда
		
		ДобавитьСкопироватьИзменитьРегламентноеЗаданиеГашение("Изменить");
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаданийГашениеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ДобавитьСкопироватьИзменитьРегламентноеЗаданиеГашение(?(Копирование, "Скопировать", "Добавить"));

КонецПроцедуры

&НаКлиенте
Процедура СписокЗаданийГашениеПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	Если Элементы.СписокЗаданийГашение.ВыделенныеСтроки.Количество() > 1 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Выберите одно регламентное задание.'"));
	Иначе
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ТаблицаРегламентныеЗаданияГашениеПередУдалениемЗавершение", ЭтотОбъект),
			НСтр("ru = 'Удалить регламентное задание?'"), РежимДиалогаВопрос.ДаНет);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРегламентныеЗаданияГашениеПередУдалениемЗавершение(Ответ, Контекст) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		УдалитьРегламентноеЗаданиеГашениеВыполнитьНаСервере(
			Элементы.СписокЗаданийГашение.ТекущиеДанные.Идентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьРегламентноеЗаданиеГашениеВыполнитьНаСервере(Знач Идентификатор)
	
	Строка = СписокЗаданийГашение.НайтиСтроки(Новый Структура("Идентификатор", Идентификатор))[0];
	кб99_РегламентныеЗадания.УдалитьРегламентноеЗадание(Идентификатор);
	СписокЗаданийГашение.Удалить(СписокЗаданийГашение.Индекс(Строка));
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаданийГашениеПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОтправкаВСДТранзакция

&НаКлиенте
Процедура ДобавитьСкопироватьИзменитьРегламентноеЗаданиеОтправкаВСДТранзакции(Знач Действие)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Действие", Действие);
	
	Если Действие <> "Добавить" Тогда
		Если Элементы.СписокЗаданийОтправкаВСДТранзакции.ТекущиеДанные = Неопределено Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Выберите регламентное задание.'"));
			Возврат;
		Иначе
			ПараметрыФормы.Вставить("Идентификатор", Элементы.СписокЗаданийОтправкаВСДТранзакции.ТекущиеДанные.Идентификатор);
		КонецЕсли;
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.кб99_РегламентноеЗаданиеОтправкаВСДТранзакции", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЗаданиеОтправкаВСДТранзакция(Команда)
	
	ДобавитьСкопироватьИзменитьРегламентноеЗаданиеОтправкаВСДТранзакции("Добавить");
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаданийОтправкаВСДТранзакцииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СписокЗаданийОтправкаВСДТранзакцииИспользование"
					ИЛИ Поле.Имя = "СписокЗаданийОтправкаВСДТранзакцииНаименование"	
					ИЛИ Поле.Имя = "СписокЗаданийОтправкаВСДТранзакцииИдентификатор"	
					ИЛИ Поле.Имя = "СписокЗаданийОтправкаВСДТранзакцииРасписание" Тогда
		
		ДобавитьСкопироватьИзменитьРегламентноеЗаданиеОтправкаВСДТранзакции("Изменить");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаданийОтправкаВСДТранзакцииПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)

	Отказ = Истина;
	
	ДобавитьСкопироватьИзменитьРегламентноеЗаданиеОтправкаВСДТранзакции(?(Копирование, "Скопировать", "Добавить"));

КонецПроцедуры

&НаКлиенте
Процедура СписокЗаданийОтправкаВСДТранзакцииПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	Если Элементы.СписокЗаданийОтправкаВСДТранзакции.ВыделенныеСтроки.Количество() > 1 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Выберите одно регламентное задание.'"));
	Иначе
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ТаблицаРегламентныеЗаданияОтправкаВСДПередУдалениемЗавершение", ЭтотОбъект),
			НСтр("ru = 'Удалить регламентное задание?'"), РежимДиалогаВопрос.ДаНет);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРегламентныеЗаданияОтправкаВСДПередУдалениемЗавершение(Ответ, Контекст) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		УдалитьРегламентноеЗаданиеОтправкаВСДВыполнитьНаСервере(
			Элементы.СписокЗаданийОтправкаВСДТранзакции.ТекущиеДанные.Идентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьРегламентноеЗаданиеОтправкаВСДВыполнитьНаСервере(Знач Идентификатор)
	
	Строка = СписокЗаданийОтправкаВСДТранзакции.НайтиСтроки(Новый Структура("Идентификатор", Идентификатор))[0];
	кб99_РегламентныеЗадания.УдалитьРегламентноеЗадание(Идентификатор);
	СписокЗаданийОтправкаВСДТранзакции.Удалить(СписокЗаданийОтправкаВСДТранзакции.Индекс(Строка));
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаданийОтправкаВСДТранзакцииПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти 

#Область Производство

&НаКлиенте
Процедура ДобавитьСкопироватьИзменитьРегламентноеЗаданиеПроизводство(Знач Действие)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Действие", Действие);
	
	Если Действие <> "Добавить" Тогда
		Если Элементы.СписокЗаданийПроизводство.ТекущиеДанные = Неопределено Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Выберите регламентное задание.'"));
			Возврат;
		Иначе
			ПараметрыФормы.Вставить("Идентификатор", Элементы.СписокЗаданийПроизводство.ТекущиеДанные.Идентификатор);
		КонецЕсли;
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.кб99_РегламентноеЗаданиеПроизводство", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаданийПроизводствоВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СписокЗаданийПроизводствоИспользование"
				ИЛИ Поле.Имя = "СписокЗаданийПроизводствоНаименование"	
				ИЛИ Поле.Имя = "СписокЗаданийПроизводствоИдентификатор"	
				ИЛИ Поле.Имя = "СписокЗаданийПроизводствоРасписание" Тогда
		
		ДобавитьСкопироватьИзменитьРегламентноеЗаданиеПроизводство("Изменить");
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СписокЗаданийПроизводствоПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ДобавитьСкопироватьИзменитьРегламентноеЗаданиеПроизводство(?(Копирование, "Скопировать", "Добавить"));

КонецПроцедуры

&НаКлиенте
Процедура СписокЗаданийПроизводствоПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаданийПроизводствоПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	Если Элементы.СписокЗаданийПроизводство.ВыделенныеСтроки.Количество() > 1 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Выберите одно регламентное задание.'"));
	Иначе
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ТаблицаРегламентныеЗаданияПроизводствоЗавершение", ЭтотОбъект),
			НСтр("ru = 'Удалить регламентное задание?'"), РежимДиалогаВопрос.ДаНет);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЗаданиеПроизводство(Команда)
	
	ДобавитьСкопироватьИзменитьРегламентноеЗаданиеПроизводство("Добавить");
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРегламентныеЗаданияПроизводствоЗавершение(Ответ, Контекст) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		УдалитьРегламентноеЗаданиеПроизводствоНаСервере(
			Элементы.СписокЗаданийПроизводство.ТекущиеДанные.Идентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьРегламентноеЗаданиеПроизводствоНаСервере(Знач Идентификатор)
	
	Строка = СписокЗаданийПроизводство.НайтиСтроки(Новый Структура("Идентификатор", Идентификатор))[0];
	кб99_РегламентныеЗадания.УдалитьРегламентноеЗадание(Идентификатор);
	СписокЗаданийПроизводство.Удалить(СписокЗаданийПроизводство.Индекс(Строка));
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти
