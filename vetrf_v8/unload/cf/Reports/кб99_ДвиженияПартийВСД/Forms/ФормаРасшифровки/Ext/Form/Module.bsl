
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Схема = Отчеты.кб99_ДвиженияПартийВСД.ПолучитьМакет("РасшифровкаДокументыДвижения");
	
	Настройки = Схема.НастройкиПоУмолчанию;
	
	Для Каждого СтрокаОтбора Из Параметры.Отбор Цикл
		
		Отбор = Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(СтрокаОтбора.Ключ);
		Отбор.ПравоеЗначение = СтрокаОтбора.Значение;
		
	КонецЦикла;
	
	ДанныеРасшифровкиОтчета = Новый ДанныеРасшифровкиКомпоновкиДанных;
	
	//Формируем макет, с помощью компоновщика макета
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	//Передаем в макет компоновки схему, настройки и данные расшифровки
	МакетКомпоновки = КомпоновщикМакета.Выполнить(Схема, Настройки, ДанныеРасшифровкиОтчета);
	
	//Выполним компоновку с помощью процессора компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки,,ДанныеРасшифровкиОтчета);
	
	//Очищаем поле табличного документа
	Результат.Очистить();
	
	// На форме есть реквизит "ДанныеРасшифровки" с типом "Строка"
	// присвоим адрес хранилища наших данных расшифровок этой переменной
	ДанныеРасшифровки = ПоместитьВоВременноеХранилище(ДанныеРасшифровкиОтчета, УникальныйИдентификатор);
	
	//Выводим результат в табличный документ
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(Результат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	АвтоЗаголовок = Ложь;
	Заголовок = НСтр("ru = 'Движения (расшифровка)'");
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Регистратор = ПолеРасшифровкиОтчетаДвижениеМаркированногоТовара(Расшифровка);
	Если Регистратор <> Неопределено Тогда
		ПоказатьЗначение(Неопределено, Регистратор);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолеРасшифровкиОтчетаДвижениеМаркированногоТовара(Расшифровка)
	
	ПоляРасшифровки = ПолучитьИзВременногоХранилища(ДанныеРасшифровки).Элементы.Получить(Расшифровка).ПолучитьПоля();
	
	НайденноеПоле = ПоляРасшифровки.Найти("Партия");
	Если НайденноеПоле = Неопределено Тогда
		НайденноеПоле = ПоляРасшифровки.Найти("Документ");
		Если НайденноеПоле = Неопределено Тогда
			Возврат Неопределено;
		Иначе
			Возврат НайденноеПоле.Значение;
		КонецЕсли;
	Иначе
		Возврат НайденноеПоле.Значение;
	КонецЕсли;
	
КонецФункции

